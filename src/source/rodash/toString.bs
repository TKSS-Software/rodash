namespace rodash
  ' Attempts to convert the supplied value to a string.
  ' @since 0.0.21
  ' @category Lang
  ' @param {Dynamic} value The value to convert.
  ' @return {String} Results of the conversion.
  function toString(value as Dynamic) as String
    if rodash.isString(value) then return value
    if rodash.isNumber(value) then return rodash.internal.numberToString(value)
    if rodash.isNode(value) then return rodash.internal.nodeToString(value)
    if rodash.isBoolean(value) then return rodash.internal.booleanToString(value)
    if rodash.isAA(value) then return rodash.internal.aaToString(value)
    if rodash.isArray(value) then return rodash.internal.arrayToString(value)
    return ""
  end function
end namespace
