namespace rodash
  ' Creates a slice of array excluding elements dropped from the beginning. Elements are dropped until predicate returns falsey. The predicate is invoked with three arguments: (value, index, array).
  ' @since 0.0.21
  ' @category Array
  ' @param {Array} array - The array to query
  ' @param {Dynamic} predicate - The function invoked per iteration
  ' @returns {Array} Returns the slice of array
  function dropWhile(array = CreateObject("roArray", 0, true) as Object, predicate = Invalid)
    array = rodash.clone(array)
    for i = 0 to array.count() - 1
      item = array[i]

      if rodash.isFunction(predicate) then
        if NOT predicate(item) then return rodash.slice(array, i)
      else if rodash.isAA(predicate) then
        if NOT rodash.isEqual(item, predicate) then return rodash.slice(array, i)
      else if rodash.isArray(predicate) then
        if NOT rodash.isEqual(item[predicate[0]], predicate[1]) then return rodash.slice(array, i)
      else if rodash.isString(predicate) then
        if NOT item[predicate] then return rodash.slice(array, i)
      end if
    end for

    return array
  end function
end namespace
