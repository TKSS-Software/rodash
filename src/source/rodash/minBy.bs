namespace rodash
  ' Computes the minimum value of array. If array is empty or falsey, invalid is returned.
  ' @since 0.0.21
  ' @category Math
  ' @param {Array} array - The array to iterate over
  ' @returns {Dynamic} Returns the maximum value
  function minBy(array = CreateObject("roArray", 0, true) as Object, iteratee = Invalid) as Dynamic
    if rodash.isEmpty(array) then return Invalid

    minValue = Invalid
    if rodash.isInvalid(iteratee) then
      minValue = rodash.internal.getConstants().max_int
      for each value in array
        if rodash.lt(value, minValue) then minValue = value
      end for
    else if rodash.isFunction(iteratee) AND rodash.isAA(array[0]) then
      minValue = array[0]
      for each value in array
        if rodash.lt(iteratee(value), iteratee(minValue)) then minValue = value
      end for
    else if rodash.isString(iteratee) AND rodash.isAA(array[0]) then
      minValue = array[0]
      for each value in array
        if rodash.lt(value[iteratee], minValue[iteratee]) then minValue = value
      end for
    end if

    return minValue
  end function
end namespace
