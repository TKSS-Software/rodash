namespace rodash
  ' Creates an array of values corresponding to paths of object.
  ' @since 0.0.21
  ' @category Object
  ' @param {AssocArray} obj - The object to iterate over.
  ' @param {Array} paths - The property paths to pick.
  ' @returns {Array} Returns the picked values.
  ' @example
  ' rodash.at({a: {b: 2}}, ["a.b"])
  ' // [2]
  ' rodash.at({a: {b: 2}}, ["a.b", "a.c"])
  ' // [2, invalid]
  function at(obj as Object, paths as Object)
    if NOT rodash.isNonEmptyAA(obj) OR NOT rodash.isNonEmptyArray(paths) then return CreateObject("roArray", 0, true)
    returnArray = CreateObject("roArray", paths.count(), true)

    for each path in paths
      result = rodash.get(obj, path)
      returnArray.push(result)
    end for

    return returnArray
  end function
end namespace
