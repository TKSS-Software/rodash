namespace rodash
  ' * This method is like rodash.difference except that it accepts iteratee which is invoked for each element of array and values to generate the criterion by which they're compared. The order and references of result values are determined by the first array. The iteratee is invoked with one argument:(value).
  ' * @since 0.0.21
  ' * @category Array
  ' * @param {Array} array - The array to inspect
  ' * @param {Array} values - The values to exclude
  ' * @param {Dynamic} iteratee - The iteratee invoked per element
  ' * @returns {Array} returnArray - Returns the new array of filtered values
  function differenceBy(array = [] as object, values = [] as object, iteratee = invalid) as object
    iterateeIsFunction = rodash.isFunction(iteratee)
    iterateeIsProperty = NOT iterateeIsFunction AND rodash.isString(iteratee)

    returnArray = []
    for each item in array
      convertedItem = item
      if iterateeIsFunction then
        convertedItem = iteratee(item)
      else if iterateeIsProperty then
        convertedItem = item[iteratee]
      end if
      found = false

      for each valueToMatch in values
        if iterateeIsFunction then
          valueToMatch = iteratee(valueToMatch)
        else if iterateeIsProperty
          valueToMatch = valueToMatch[iteratee]
        end if
        if convertedItem = valueToMatch then found = true : exit for
      end for

      if NOT found then returnArray.push(item)
    end for

    return returnArray
  end function
end namespace
