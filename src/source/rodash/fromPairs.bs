namespace rodash
    ' The inverse of rodash.toPairs; this method returns an object composed from key-value pairs.
    ' @since 0.0.24
    ' @category Array
    ' @param {Array} pairs - And array of arrays to be converted to an object
    ' @return {Object} Returns the new object
    function fromPairs(pairs = CreateObject("roArray", 0, true) as object) as object
      returnObject = {}
      if not rodash.isArray(pairs) then return returnObject

      for each pair in pairs
        if rodash.isArray(pair) and pair.count() = 2 then
          returnObject[pair[0]] = pair[1]
        end if
      end for

      return returnObject
    end function
  end namespace