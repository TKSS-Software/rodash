import "isBoolean.bs"
import "isString.bs"
import "isNumber.bs"
import "stringIncludes.bs"

namespace rodash
  ' /**
  ' * @name toNumber
  ' * @description Attempts to convert the supplied value into a valid number
  ' * @param {Dynamic} value The variable to be converted
  ' * @return {Dynamic} Results of the conversion
  ' */
  function toNumber(value as Dynamic) as Dynamic
    if rodash.isNumber(value) then
      return value
    else if rodash.isBoolean(value) then
      if value then return 1
      return 0
    end if

    if rodash.isString(value) then
      ' TODO: Temporary fix until we figure a better way to avoid val converting 8037667 to 8.03767e+06
      if rodash.stringIncludes(value, ".") then
        return val(value)
      else
        return val(value, 10)
      end if
    end if

    return 0
  end function
end namespace