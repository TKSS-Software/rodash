namespace rodash
  ' Creates a new array concatenating array with any additional arrays and/or values.
  ' @since 0.0.21
  ' @category Array
  ' @param {Array} array - The array to concatenate
  ' @param {Array} values - The values to concatenate
  ' @returns {Array} Returns the new concatenated array
  ' @example
  ' rodash.concat([1], [2, [3], [[4]]]) // => [1, 2, [3], [[4]]]
  function concat(array as Object, values as Object) as Object
    if NOT rodash.isArray(array) then return CreateObject("roArray", 0, true)
    if NOT rodash.isArray(values) then return array

    returnArray = rodash.clone(array)

    for each value in values
      if rodash.isArray(value) then
        returnArray.append(value)
      else
        returnArray.push(value)
      end if
    end for

    return returnArray
  end function
end namespace
