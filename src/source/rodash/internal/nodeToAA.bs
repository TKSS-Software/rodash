
namespace rodash.internal
  ' @ignore
  ' Attempts to converts a nodes top level fields to an AssociativeArray.
  ' @since 0.0.21
  ' @category Internal
  ' @param {Dynamic} value - The variable to be converted.
  ' @param {Boolean} removeId - If set to true the nodes ID will also be stripped.
  ' @param {Object} removeFields - List of keys that need to be removed from the node.
  ' @return {Dynamic} Results of the conversion.
  function nodeToAA(value as Object, removeId = false as Boolean, removeFields = [] as Object) as Object
    if rodash.isNode(value) then
      fields = value.getFields()
      fields.delete("change")
      fields.delete("focusable")
      fields.delete("focusedChild")
      fields.delete("ready")
      if removeId then fields.delete("id")
      'Looping through any additional fields if passed.
      if rodash.isNonEmptyArray(removeFields) then
        for each field in removeFields
          fields.delete(field)
        end for
      end if
      return fields
    else if rodash.isAA(value) then
      return value
    end if

    return {}
  end function
end namespace
