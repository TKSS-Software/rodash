namespace rodash
  ' Assigns own enumerable string keyed properties of source objects to the destination object. Source objects are applied from left to right. Subsequent sources overwrite property assignments of previous sources.
  ' This method mutates object and is loosely based on lodash Object.assign.
  ' @since 0.0.21
  ' @category Object
  ' @param {Dynamic} baseAA - The destination object
  ' @params {Object} sources - The source objects
  ' @params {Dynamic} Mutaded baseAA
  function assign(baseAA as Dynamic, sources = invalid as dynamic) as Dynamic
    if NOT rodash.isAA(baseAA) then return Invalid

    if rodash.isNonEmptyArray(sources) then
      for each source in sources
        if rodash.isNonEmptyAA(source) then
          baseAA.append(source)
        end if
      end for
    end if

    return baseAA
  end function
end namespace
