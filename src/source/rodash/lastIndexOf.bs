namespace rodash
  ' * This method is like _.indexOf except that it iterates over elements of array from right to left.
  ' * @since 0.0.23
  ' * @category Array
  ' * @param {Array} array - The array to query
  ' * @param {Dynamic} value - The value to search for
  ' * @param {Integer} fromIndex - The index to search from
  ' * @return {Dynamic} Returns the index of the matched value, else -1
    function lastIndexOf(array = [] as object, value = invalid as dynamic, fromIndex = invalid as dynamic) as dynamic
      if NOT rodash.isArray(array) then return -1

      if rodash.isInvalid(fromIndex) then
          fromIndex = array.count() - 1
      else if fromIndex < 0 then
          fromIndex = array.count() + fromIndex
      end if

      ' Ensure fromIndex is within valid bounds
      fromIndex = rodash.clamp(fromIndex, 0, array.count() - 1)

      for i = fromIndex to 0 step -1
          if rodash.isEqual(array[i], value) then return i
      end for

      return -1
  end function
end namespace
