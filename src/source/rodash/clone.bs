
namespace rodash
  ' /**
  ' * @name clone
  ' * @description Clones objects that can be cloned.
  ' * @param {Dynamic} value - The value to be cloned
  ' * @return {Dynamic} The cloned value
  ' */
  function clone(value = invalid as dynamic) as dynamic
    if rodash.isInvalid(value) return invalid

    clonedValue = invalid
    if rodash.isString(value) then
      clonedValue = "" + value
    else if rodash.isAA(value) then
      clonedValue = CreateObject("roAssociativeArray")
      clonedValue.append(value)
    else if rodash.isArray(value) then
      clonedValue = CreateObject("roArray", value.count(), true)
      clonedValue.append(value)
    else if rodash.isNumber(value) then
      clonedValue = 0 + value
    else if rodash.isBoolean(value) then
      clonedValue = false OR value
    else if rodash.isNode(value) then
      clonedValue = value.clone(true)
    end if

    return clonedValue
  end function
end namespace
