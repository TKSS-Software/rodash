namespace rodash
  ' Creates a shallow clone of value.
  ' @since 0.0.21
  ' @category Lang
  ' @param {Dynamic} value - The value to be cloned
  ' @returns {Dynamic} The cloned value
  function clone(value = Invalid as Dynamic) as Dynamic
    if rodash.isInvalid(value) return Invalid

    clonedValue = Invalid
    if rodash.isArray(value) then
      clonedValue = CreateObject("roArray", value.count(), true)
      clonedValue.append(value)
    else if rodash.isAA(value) then
      clonedValue = CreateObject("roAssociativeArray")
      clonedValue.append(value)
    else if rodash.isNode(value) then
      clonedValue = value.clone(true)
    else if rodash.isString(value) then
      clonedValue = "" + value
    else if rodash.isNumber(value) then
      clonedValue = 0 + value
    else if rodash.isBoolean(value) then
      clonedValue = false OR value
    end if

    return clonedValue
  end function
end namespace
