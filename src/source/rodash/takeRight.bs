namespace rodash
  ' Creates a slice of array with n elements taken from the end
  ' @since 0.0.21
  ' @category Array
  ' @param {Array} array - The sorted array to query
  ' @param {Integer} n - The number of elements to take
  ' @returns {Object} Returns the slice of array
  function takeRight(array = CreateObject("roArray", 0, true) as Object, n = Invalid as Dynamic) as Object
    if rodash.isInvalid(n) then n = 1
    if NOT rodash.isNonEmptyArray(array) OR n = 0 then return CreateObject("roArray", 0, true)

    length = array.count()
    startPos = length - n
    if startPos < 0 then startPos = 0
    return rodash.slice(array, startPos, length)
  end function
end namespace
