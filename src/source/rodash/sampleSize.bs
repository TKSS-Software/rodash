namespace rodash
  ' * Gets n random elements at unique keys from collection up to the size of collection.
  ' * @since 0.0.23
  ' * @category Collection
  ' * @param {Dynamic} collection - The collection to sample
  ' * @param {Integer} n - The number of elements to sample
  ' * @returns {Dynamic} - Returns the random elements.
  function sampleSize(collection as Object, n as Integer) as Object
    ' Create a copy of the original array to avoid modifying it
    copyCollection = rodash.clone(collection)

    ' Create an array to store the sampled elements
    result = []

    ' Determine the smaller value between n and the length of the array
    count = copyCollection.count()
    if n < count
        count = n
    end if

    while result.count() < count
        index = rodash.random(0, copyCollection.count() - 1)
        result.push(copyCollection[index])
        copyCollection.delete(index)
    end while

    return result
  end function
end namespace
