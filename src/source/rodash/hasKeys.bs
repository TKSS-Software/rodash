
namespace rodash
  ' /**

  ' * Checks if first level of the supplied AssociativeArray contains the Array of key strings.
  ' * @param {Dynamic} aaValue - AssociativeArray to be checked
  ' * @return {Dynamic} keys - Array of key strings
  ' */
  function hasKeys(aaValue as dynamic, keys as dynamic) as boolean
    if NOT rodash.internal.isKeyedValueType(aaValue) OR aaValue.isEmpty() OR NOT rodash.isArray(keys) OR keys.isEmpty() then return false

    hasKeys = true
    for each key in keys
      if NOT aaValue.doesExist(key) then
        hasKeys = false
        exit for
      end if
    end for

    return hasKeys
  end function
end namespace
