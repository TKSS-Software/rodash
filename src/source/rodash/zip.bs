namespace rodash
  ' Creates an array of grouped elements, the first of which contains the first elements of the given arrays, the second of which contains the second elements of the given arrays, and so on.
  ' @since 0.0.21
  ' @category Array
  ' @param {Array} arrays - The property identifiers
  ' @return {Object} Returns the new array of grouped elements
  function zip(arrays = CreateObject("roArray", 0, true) as object) as object
    returnArray = CreateObject("roArray", 0, true)

    for i = 0 to arrays.count() - 1
      array = arrays[i]
      for ii = 0 to array.count() - 1
        if rodash.isInvalid(returnArray[ii]) then returnArray[ii] = CreateObject("roArray", 0, true)
        returnArray[ii].push(array[ii])
      end for
    end for

    return returnArray
  end function
end namespace
