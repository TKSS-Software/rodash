namespace rodash
  ' Checks if the supplied values are the same.
  ' By default, when comparing arrays and associative arrays the function will compare the values on the elements. If the strict parameter is set to true, the function will compare the references of the elements.
  ' @since 0.0.21
  ' @category Lang
  ' @param {Dynamic} valueOne - First value.
  ' @param {Dynamic} valueTwo - Second value.
  ' @returns {Boolean} True if the values are the same and false if not or if any of the values are a type that could not be compared.
  ' @example
  ' rodash.isEqual(1, 1) // => true
  ' rodash.isEqual(1, 2) // => false
  ' rodash.isEqual([], []) // => true
  ' rodash.isEqual({}, {}) // => true
  ' rodash.isEqual({a: 1}, {a: 1}) // => true
  ' rodash.isEqual({a: 1}, {a: 2}) // => false
  ' rodash.isEqual("Hello", "Hello") // => true
  ' rodash.isEqual("Hello", "World") // => false
  function isEqual(valueOne as Dynamic, valueTwo as Dynamic, strict = false as Boolean) as Boolean
    ' If the first argument is true we don't need to check the following conditionals
    if rodash.internal.canBeCompared(valueOne, valueTwo) then
      return (valueOne = valueTwo)
    else if rodash.isNode(valueOne) then
      if rodash.isNode(valueTwo) then return valueOne.isSameNode(valueTwo)
    else if rodash.isAA(valueOne) AND rodash.isAA(valueTwo) then
      if strict then
        key = `rodash_internal_key_${rodash.random(1000000, 2000000)}`
        valueOne.addReplace(key, "true")

        isSame = false
        if valueTwo.doesExist(key) then isSame = true
        valueOne.delete(key)
        return isSame
      end if

      if rodash.join(valueOne.keys(), ",") = rodash.join(valueTwo.keys(), ",") then return (formatJson(valueOne) = formatJson(valueTwo))
    else if rodash.isArray(valueOne) then
      if strict then
        key = `rodash_internal_key_${rodash.random(1000000, 2000000)}`
        valueOne.push(key)

        isSame = rodash.isEqual(rodash.last(valueTwo), key)
        valueOne.pop()
        return isSame
      end if

      if rodash.isArray(valueTwo) AND (valueOne.count() = valueTwo.count()) then return (formatJson(valueOne) = formatJson(valueTwo))
    end if

    return false
  end function
end namespace
