namespace rodash
  ' Invokes sub after wait milliseconds. Any additional arguments are provided to sub when it's invoked.
  ' @since 0.0.22
  ' @category Function
  ' @param {Sub} callback - The sub to be called after a set delay
  ' @param {Float} [wait] - The number of milliseconds to delay invocation
  ' @param {Dynamic} context - A single item of data to be passed into the callback when invoked
  sub delay(callback as Function, wait = 0 as Float, context = Invalid as Dynamic)
    ' Convert ms -> seconds for Timer; use minimal non-zero duration for 0
    duration = 0.0001
    if wait <> 0 then
      duration = wait / 1000.0
    end if

    timer = createObject("RoSGNode", "Timer")
    timer.update({
      duration: duration
      repeat: false
      id: "__delay_" + createObject("roDeviceInfo").getRandomUUID()
    })

    id = timer.id

    container = {
      timer: timer
      callback: callback
      context: context
    }

    m[id] = container

    timer.observeFieldScoped("fire", (sub(event as Object)
      node = event.getRoSgNode()
      localId = node.id

      ' Guard in case entry was already cleared
      if NOT m.doesExist(localId) then return

      localContainer = m[localId]
      cb = localContainer.callback
      ctx = localContainer.context

      try
        cb(ctx)
      catch e
        print "Crash during utils.delay:"
        print e
      end try

      node.unobserveFieldScoped("fire")
      m.delete(localId)
    end sub).toStr().mid(10))

    timer.control = "start"
  end sub
end namespace
