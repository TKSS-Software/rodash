namespace rodash
  ' /**
  ' * Invokes func after wait milliseconds. Any additional arguments are provided to func when it's invoked.
  ' @param {Function} callback - The function to be called after a set delay
  ' @param {Float} [duration] - the amount of delay before invoking the callback
  ' @param {Dynamic} context - a single item of data to be passed into the callback when invoked
  ' */
  sub delay(callback as function, duration = 0.0001 as float, context = invalid as dynamic)
    timer = createObject("RoSGNode", "Timer")
    timer.update({duration: duration, repeat: false, id: "__delay_" + createObject("roDeviceInfo").getRandomUUID() })

    m[timer.id] = {
      timer: timer
      callback: callback
      context: context
    }

    timer.observeFieldScoped("fire", sub (event as object)
      delayId = event.getNode()
      options = m[delayId]
      callback = options.callback
      try
        callback(options.context)
      catch e
        print "Crash during utils.delay:", e
      end try
      m[delayId].timer.unobserveFieldScoped("fire")
      m.delete(delayId)
    end sub)

    timer.control = "start"
  end sub
end namespace
