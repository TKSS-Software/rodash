namespace rodash
  ' Creates an array with all falsey values removed. The values false, 0, "", and invalid are falsey.
  ' @since 0.0.21
  ' @category Array
  ' @param {Array} array - The array to compact
  ' @returns {Array} Returns the new array of filtered values
  ' @example
  ' rodash.compact([0, 1, false, 2, '', 3]) // => [1, 2, 3]
  function compact(array as Object) as Object
    if NOT rodash.isNonEmptyArray(array) then return array

    returnArray = CreateObject("roArray", 0, true)

    if rodash.isNonEmptyArray(array) then
      for each item in array
        if rodash.isInvalid(item) OR rodash.isEmptyString(item) then
          continue for
        else if rodash.isNumber(item) then
          if item = 0 then
            continue for
          end if
        else if rodash.isBoolean(item) then
          if item = false then
            continue for
          end if
        end if

        returnArray.push(item)
      end for
    end if

    return returnArray
  end function
end namespace
