
namespace rodash
  '/**
  ' * @name slice
  ' * @description Creates a slice of array from start up to, but not including, end.
  ' * @param {Array} array - The array to slice
  ' * @param {Integer} startPos - The start position
  ' * @param {Integer} endPos - The end position
  ' * @return {Dynamic} Returns the slice of array
  ' */
  function slice(array = [] as object, startPos = 0, endPos = invalid)
    if NOT rodash.isArray(array) then return invalid
    if rodash.isNotInvalid(endPos) then endPos = endPos - 1

    array = rodash.clone(array)
    arraySize = array.count()
    lastIndex = arraySize - 1
    slicedArray = []

    if startPos < 0 then startPos = arraySize + startPos
    if endPos = invalid then endPos = lastIndex
    if endPos < 0 then endPos = arraySize + endPos
    if endPos >= arraySize then endPos = lastIndex

    if startPos >= arraySize OR startPos > endPos then return slicedArray

    for i = startPos to endPos
        slicedArray.push(array[i])
    end for

    return slicedArray
  end function
end namespace
