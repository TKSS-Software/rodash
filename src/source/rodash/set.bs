
namespace rodash
  ' /**

  ' * Used to set a nested String value in the supplied object
  ' * @param {Object} aa - Object to drill down into.
  ' * @param {String} keyPath - A dot notation based string to the expected value.
  ' * @param {Dynamic} value - The value to be set.
  ' * @return {Boolean} True if set successfully.
  ' */
  function set(aa as Object, keyPath as String, value as Dynamic) as Boolean
    if NOT rodash.isAA(aa) then return false

    level = aa
    keys = rodash.internal.sanitizeKeyPath(keyPath).tokenize(".")
    while keys.count() > 1
      key = keys.shift()
      if NOT rodash.isAA(level[key]) then level[key] = {}
      level = level[key]
    end while

    finalKey = keys.shift()
    level[finalKey] = value
    return true
  end function
end namespace
