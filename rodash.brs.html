

<!DOCTYPE html>
<html lang="en">

<head>
  
  <meta charset="utf-8">
  <meta name="viewport" content="width=device-width, initial-scale=1">
  <title>TKSS Rodash Documentation rodash.brs</title>

  <script src="https://cdn.jsdelivr.net/gh/google/code-prettify@master/loader/run_prettify.js"></script>
  <script src="https://unpkg.com/@babel/standalone/babel.min.js"></script>
  <script src="./build/entry.js"></script>
  <script src="https://ajax.googleapis.com/ajax/libs/jquery/3.3.1/jquery.min.js"></script>
  <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
  <![endif]-->
  <link href="https://fonts.googleapis.com/css?family=Roboto:100,400,700|Inconsolata,700" rel="stylesheet">
  <link rel="stylesheet" href="https://use.fontawesome.com/releases/v5.6.3/css/all.css" integrity="sha384-UHRtZLI+pbxtHCWp1t77Bi1L4ZtiqrqD80Kn4Z8NTSRyMA2Fd33n5dQ8lWUE00s/" crossorigin="anonymous">
  <link type="text/css" rel="stylesheet" href="https://jmblog.github.io/color-themes-for-google-code-prettify/themes/tomorrow-night.min.css">
  <link type="text/css" rel="stylesheet" href="styles/app.min.css">
  <link type="text/css" rel="stylesheet" href="styles/iframe.css">
  <link type="text/css" rel="stylesheet" href="">
  <script async defer src="https://buttons.github.io/buttons.js"></script>

  
</head>



<body class="layout small-header">
    <div id="stickyNavbarOverlay"></div>
    

<div class="top-nav">
    <div class="inner">
        <a id="hamburger" role="button" class="navbar-burger" aria-label="menu" aria-expanded="false">
            <span aria-hidden="true"></span>
            <span aria-hidden="true"></span>
            <span aria-hidden="true"></span>
        </a>
        <div class="logo">
            
             
                <a href="index.html">
                    <h1 class="navbar-item">TKSS Rodash Documentation</h1>
                </a>
            
        </div>
        <div class="menu">
            
            <div class="navigation">
                <a
                    href="index.html"
                    class="link"
                >
                    Documentation
                </a>
                
                 
                    
                        <a
                            class="link user-link "
                            href="https://github.com/TKSS-Software/rodash"
                        >
                            Github
                        </a>
                    
                
                
            </div>
        </div>
    </div>
</div>
    <div id="main">
        <div
            class="sidebar "
            id="sidebarNav"
        >
            
                <div class="search-wrapper">
                    <input id="search" type="text" placeholder="Search docs..." class="input">
                </div>
            
            <nav>
                
                    <h2><a href="index.html">Documentation</a></h2><div class="category"></div><div class="category"><h2>Array</h2><h3>Global</h3><ul><li><a href="global.html#chunk">chunk</a></li><li><a href="global.html#compact">compact</a></li><li><a href="global.html#concat">concat</a></li><li><a href="global.html#difference">difference</a></li><li><a href="global.html#differenceBy">differenceBy</a></li><li><a href="global.html#differenceWith">differenceWith</a></li><li><a href="global.html#drop">drop</a></li><li><a href="global.html#dropRight">dropRight</a></li><li><a href="global.html#dropRightWhile">dropRightWhile</a></li><li><a href="global.html#dropWhile">dropWhile</a></li><li><a href="global.html#fill">fill</a></li><li><a href="global.html#findIndex">findIndex</a></li><li><a href="global.html#findLastIndex">findLastIndex</a></li><li><a href="global.html#first">first</a></li><li><a href="global.html#flatten">flatten</a></li><li><a href="global.html#flattenDeep">flattenDeep</a></li><li><a href="global.html#flattenDepth">flattenDepth</a></li><li><a href="global.html#fromPairs">fromPairs</a></li><li><a href="global.html#head">head</a></li><li><a href="global.html#indexOf">indexOf</a></li><li><a href="global.html#initial">initial</a></li><li><a href="global.html#intersection">intersection</a></li><li><a href="global.html#intersectionBy">intersectionBy</a></li><li><a href="global.html#intersectionWith">intersectionWith</a></li><li><a href="global.html#join">join</a></li><li><a href="global.html#last">last</a></li><li><a href="global.html#lastIndexOf">lastIndexOf</a></li><li><a href="global.html#orderBy">orderBy</a></li><li><a href="global.html#slice">slice</a></li><li><a href="global.html#sortedIndex">sortedIndex</a></li><li><a href="global.html#take">take</a></li><li><a href="global.html#takeRight">takeRight</a></li><li><a href="global.html#toArray">toArray</a></li><li><a href="global.html#toPairs">toPairs</a></li><li><a href="global.html#union">union</a></li><li><a href="global.html#uniq">uniq</a></li><li><a href="global.html#zip">zip</a></li><li><a href="global.html#zipObject">zipObject</a></li></ul></div><div class="category"><h2>Collection</h2><h3>Global</h3><ul><li><a href="global.html#find">find</a></li><li><a href="global.html#forEach">forEach</a></li><li><a href="global.html#forEachRight">forEachRight</a></li><li><a href="global.html#groupBy">groupBy</a></li><li><a href="global.html#keyBy">keyBy</a></li><li><a href="global.html#map">map</a></li><li><a href="global.html#reduce">reduce</a></li><li><a href="global.html#sample">sample</a></li><li><a href="global.html#sampleSize">sampleSize</a></li><li><a href="global.html#shuffle">shuffle</a></li><li><a href="global.html#size">size</a></li><li><a href="global.html#sortBy">sortBy</a></li></ul></div><div class="category"><h2>Date</h2><h3>Global</h3><ul><li><a href="global.html#asDateString">asDateString</a></li><li><a href="global.html#asSeconds">asSeconds</a></li><li><a href="global.html#fromISO8601String">fromISO8601String</a></li><li><a href="global.html#fromSeconds">fromSeconds</a></li><li><a href="global.html#getDayOfMonth">getDayOfMonth</a></li><li><a href="global.html#getDayOfWeek">getDayOfWeek</a></li><li><a href="global.html#getHours">getHours</a></li><li><a href="global.html#getLastDayOfMonth">getLastDayOfMonth</a></li><li><a href="global.html#getMilliseconds">getMilliseconds</a></li><li><a href="global.html#getMinutes">getMinutes</a></li><li><a href="global.html#getMonth">getMonth</a></li><li><a href="global.html#getSeconds">getSeconds</a></li><li><a href="global.html#getYear">getYear</a></li><li><a href="global.html#now">now</a></li><li><a href="global.html#toISOString">toISOString</a></li></ul></div><div class="category"><h2>Function</h2><h3>Global</h3><ul><li><a href="global.html#debounce">debounce</a></li><li><a href="global.html#delay">delay</a></li></ul></div><div class="category"><h2>Lang</h2><h3>Global</h3><ul><li><a href="global.html#clone">clone</a></li><li><a href="global.html#eq">eq</a></li><li><a href="global.html#gt">gt</a></li><li><a href="global.html#gte">gte</a></li><li><a href="global.html#isAA">isAA</a></li><li><a href="global.html#isArray">isArray</a></li><li><a href="global.html#isArrayLike">isArrayLike</a></li><li><a href="global.html#isBoolean">isBoolean</a></li><li><a href="global.html#isByteArray">isByteArray</a></li><li><a href="global.html#isDate">isDate</a></li><li><a href="global.html#isDateTime">isDateTime</a></li><li><a href="global.html#isDouble">isDouble</a></li><li><a href="global.html#isElement">isElement</a></li><li><a href="global.html#isEmpty">isEmpty</a></li><li><a href="global.html#isEmptyString">isEmptyString</a></li><li><a href="global.html#isEqual">isEqual</a></li><li><a href="global.html#isEqualWith">isEqualWith</a></li><li><a href="global.html#isError">isError</a></li><li><a href="global.html#isFinite">isFinite</a></li><li><a href="global.html#isFloat">isFloat</a></li><li><a href="global.html#isFunction">isFunction</a></li><li><a href="global.html#isInteger">isInteger</a></li><li><a href="global.html#isInvalid">isInvalid</a></li><li><a href="global.html#isLength">isLength</a></li><li><a href="global.html#isMap">isMap</a></li><li><a href="global.html#isNaN">isNaN</a></li><li><a href="global.html#isNode">isNode</a></li><li><a href="global.html#isNonEmptyAA">isNonEmptyAA</a></li><li><a href="global.html#isNonEmptyArray">isNonEmptyArray</a></li><li><a href="global.html#isNonEmptyString">isNonEmptyString</a></li><li><a href="global.html#isNotInvalid">isNotInvalid</a></li><li><a href="global.html#isNull">isNull</a></li><li><a href="global.html#isNumber">isNumber</a></li><li><a href="global.html#isString">isString</a></li><li><a href="global.html#lt">lt</a></li><li><a href="global.html#lte">lte</a></li><li><a href="global.html#toNumber">toNumber</a></li><li><a href="global.html#toString">toString</a></li></ul></div><div class="category"><h2>Math</h2><h3>Global</h3><ul><li><a href="global.html#add">add</a></li><li><a href="global.html#ceil">ceil</a></li><li><a href="global.html#divide">divide</a></li><li><a href="global.html#floor">floor</a></li><li><a href="global.html#max">max</a></li><li><a href="global.html#maxBy">maxBy</a></li><li><a href="global.html#mean">mean</a></li><li><a href="global.html#meanBy">meanBy</a></li><li><a href="global.html#min">min</a></li><li><a href="global.html#minBy">minBy</a></li><li><a href="global.html#multiply">multiply</a></li><li><a href="global.html#round">round</a></li><li><a href="global.html#subtract">subtract</a></li><li><a href="global.html#sum">sum</a></li><li><a href="global.html#sumBy">sumBy</a></li></ul></div><div class="category"><h2>Number</h2><h3>Global</h3><ul><li><a href="global.html#clamp">clamp</a></li><li><a href="global.html#inRange">inRange</a></li><li><a href="global.html#random">random</a></li></ul></div><div class="category"><h2>Object</h2><h3>Global</h3><ul><li><a href="global.html#assign">assign</a></li><li><a href="global.html#at">at</a></li><li><a href="global.html#forIn">forIn</a></li><li><a href="global.html#forInRight">forInRight</a></li><li><a href="global.html#forOwn">forOwn</a></li><li><a href="global.html#forOwnRight">forOwnRight</a></li><li><a href="global.html#get">get</a></li><li><a href="global.html#getAA">getAA</a></li><li><a href="global.html#getArray">getArray</a></li><li><a href="global.html#getBoolean">getBoolean</a></li><li><a href="global.html#getNumber">getNumber</a></li><li><a href="global.html#getString">getString</a></li><li><a href="global.html#hasKeys">hasKeys</a></li><li><a href="global.html#invert">invert</a></li><li><a href="global.html#omit">omit</a></li><li><a href="global.html#pick">pick</a></li><li><a href="global.html#set">set</a></li></ul></div><div class="category"><h2>String</h2><h3>Global</h3><ul><li><a href="global.html#camelCase">camelCase</a></li><li><a href="global.html#capitalize">capitalize</a></li><li><a href="global.html#endsWith">endsWith</a></li><li><a href="global.html#escape">escape</a></li><li><a href="global.html#escapeRegExp">escapeRegExp</a></li><li><a href="global.html#kebabCase">kebabCase</a></li><li><a href="global.html#lowerCase">lowerCase</a></li><li><a href="global.html#lowerFirst">lowerFirst</a></li><li><a href="global.html#padString">padString</a></li><li><a href="global.html#paddString">paddString</a></li><li><a href="global.html#stringIncludes">stringIncludes</a></li><li><a href="global.html#stringIndexOf">stringIndexOf</a></li></ul></div><div class="category"><h2>Utils</h2><h3>Global</h3><ul><li><a href="global.html#times">times</a></li></ul></div>
                
            </nav>
        </div>
        <div class="core" id="main-content-wrapper">
            <div class="content">
                <header class="page-title">
                    <p>Source</p>
                    <h1>rodash.brs</h1>
                </header>
                



    
    <section>
        <article>
            <pre class="prettyprint source linenums"><code>' * Adds two numbers.
' * @since 0.0.21
' * @category Math
' * @param {Dynamic} augend - The first number in an addition
' * @param {Dynamic} addend - The second number in an addition
' * @returns {Dynamic} value - Returns the total
function add(augend, addend)
    value = 0
    if isNumber(augend) then
        value += augend
    end if
    if isNumber(addend) then
        value += addend
    end if
    return value
end function

' * Returns a formatted version of the current time/date.
' * @since 0.0.21
' * @category Date
' * @param {String} format - The date format
' * @returns {Object} value - Returns a object containing the formatted date for both UTC and Local time
function asDateString(format = "long-date" as string) as object
    dateObj = internal_getDateObject()
    return {
        "utc": dateObj.utc.asDateString(format)
        "local": dateObj.local.asDateString(format)
    }
end function

' * Returns the current time in seconds.
' * @since 0.0.21
' * @category Date
' * @returns {Object} value - Returns a object containing the date/time in seconds for both UTC and Local time
function asSeconds() as object
    dateObj = internal_getDateObject()
    return {
        "utc": dateObj.utc.asSeconds()
        "local": dateObj.local.asSeconds()
    }
end function

' * Assigns own enumerable string keyed properties of source objects to the destination object. Source objects are applied from left to right. Subsequent sources overwrite property assignments of previous sources.
' * This method mutates object and is loosely based on Object.assign.
' * @since 0.0.21
' * @category Object
' * @param {Dynamic} baseAA - The destination object
' * @params {Object} sources - The source objects
' * @params {Dynamic} Mutaded baseAA
function assign(baseAA as Dynamic, sources = [] as Object) as Dynamic
    if NOT isAA(baseAA) then
        return Invalid
    end if
    for each source in sources
        if isAA(source) then
            baseAA.append(source)
        end if
    end for
    return baseAA
end function

' * Creates an array of values corresponding to paths of object.
' * @since 0.0.21
' * @category Object
' * @param {AssocArray} obj - The object to iterate over.
' * @param {Array} paths - The property paths to pick.
' * @returns {Array} value - Returns the picked values.
function at(obj = {} as object, paths = [] as object)
    returnArray = []
    for each path in paths
        result = get(obj, path)
        returnArray.push(result)
    end for
    return returnArray
end function

' * Converts a string to camel case.
' * @since 0.0.21
' * @category String
' * @param {String} value - The string to convert.
' * @returns {String} - The camel case string.
function camelCase(value = "" as string)
    regex = CreateObject("roRegex", "[^a-zA-Z0-9\s]", "")
    value = regex.ReplaceAll(value, "")
    value = value.trim()
    valueArray = value.split(" ")
    responseValue = ""
    for i = 0 to valueArray.count() - 1
        valueString = valueArray[i]
        if i = 0 then
            responseValue += lcase(valueString)
        else
            responseValue += capitalize(valueString)
        end if
    end for
    return responseValue
end function

' * Capitalizes the first letter of a string.
' * @since 0.0.21
' * @category String
' * @param {String} value - The string to capitalize.
' * @returns {String} - The capitalized string.
function capitalize(value = "" as string)
    value = value.trim()
    valueArray = value.split("")
    responseValue = ""
    for i = 0 to valueArray.count() - 1
        valueString = valueArray[i]
        if i = 0 then
            responseValue += ucase(valueString)
        else
            responseValue += lcase(valueString)
        end if
    end for
    return responseValue
end function

' * Computes number rounded up to precision.
' * @since 0.0.21
' * @category Math
' * @param {Integer} number - The number to round up
' * @param {Integer} precision - The precision to round up to
' * @returns {Integer} Returns the rounded up number
function ceil(number = 0, precision = 0 as dynamic) as dynamic
    return abs(int(-number * 10 ^ precision)) / 10 ^ precision
end function

' * Creates an array of elements split into groups the length of size. If array can't be split evenly, the final chunk will be the remaining elements.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to process
' * @param {Integer} chunkSize - The length of each chunk
' * @returns {Array} returnArray - Returns the new array of chunks
function chunk(array = [] as object, chunkSize = 1 as integer) as object
    array = clone(array)
    numberOfChunks = floor(array.count() / chunkSize)
    returnArray = CreateObject("roArray", numberOfChunks, true)
    arrayIndex = 0
    for index = 0 to numberOfChunks
        chunkArray = []
        for i = 0 to chunkSize - 1
            chunkArray.push(array[arrayIndex])
            arrayIndex++
            if arrayIndex = array.count() then
                exit for
            end if
        end for
        returnArray[index] = chunkArray
        if arrayIndex = array.count() then
            exit for
        end if
    end for
    return returnArray
end function

' * Clamps number within the inclusive lower and upper bounds.
' * @since 0.0.21
' * @category Number
' * @param {Integer} number - The number to clamp
' * @param {Integer} lower - The lower bound
' * @param {Integer} upper - The upper bound
' * @return {Integer} Returns the clamped number
function clamp(number, lower, upper) as dynamic
    return max([
        lower
        min([
            upper
            number
        ])
    ])
end function

' * Creates a shallow clone of value.
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value - The value to be cloned
' * @returns {Dynamic} clonedValue - The cloned value
function clone(value = invalid as dynamic) as dynamic
    if isInvalid(value)
        return invalid
    end if
    clonedValue = invalid
    if isString(value) then
        clonedValue = "" + value
    else if isAA(value) then
        clonedValue = CreateObject("roAssociativeArray")
        clonedValue.append(value)
    else if isArray(value) then
        clonedValue = CreateObject("roArray", value.count(), true)
        clonedValue.append(value)
    else if isNumber(value) then
        clonedValue = 0 + value
    else if isBoolean(value) then
        clonedValue = false OR value
    else if isNode(value) then
        clonedValue = value.clone(true)
    end if
    return clonedValue
end function

' * Creates an array with all falsey values removed. The values false, 0, "", and invalid are falsey.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to compact
' * @returns {Array} returnArray - Returns the new array of filtered values
function compact(array = [] as object) as object
    returnArray = []
    for each item in array
        shallPass = true
        typeName = type(item)
        if isInvalid(item) then
            shallPass = false
        else if isEmptyString(item) then
            shallPass = false
        else if isNumber(item) then
            if item = 0 then
                shallPass = false
            end if
        else if isBoolean(item) then
            shallPass = item
        end if
        if shallPass then
            returnArray.push(item)
        end if
    end for
    return returnArray
end function

' * Creates a new array concatenating array with any additional arrays and/or values.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to concatenate
' * @param {Array} values - The values to concatenate
' * @returns {Array} returnArray - Returns the new concatenated array
function concat(array = [] as object, values = [] as object) as object
    returnArray = []
    returnArray.append(array)
    for each value in values
        if isArray(value) then
            returnArray.append(value)
        else
            returnArray.push(value)
        end if
    end for
    return returnArray
end function

' * Invokes func after wait milliseconds. Any additional arguments are provided to func when it's invoked.
' * @since 0.0.22
' * @category Function
' * @param {Sub} callback - The function sub to be called after a set delay
' * @param {Float} [wait] - The number of milliseconds to delay
' * @param {Object} options - The options object
' * @param {Dynamic} options[context] - The context to be used when calling the callback
' * @param {Float} options[maxWait] - The maximum time the sub is allowed to be delayed before it's invoked.
sub debounce(callback as Function, wait = 0 as dynamic, options = {
    maxWait: -1
} as object)
    id = "__debounce_" + callback.toStr()
    duration = bslib_ternary(wait = 0, 0.001, wait / 1000)
    if m[id] = invalid then
        timer = createObject("RoSGNode", "Timer")
        timer.update({
            duration: duration
            id: id
        }, true)
        m[id] = {
            callback: callback
            timer: timer
            timespan: CreateObject("roTimeSpan")
            context: get(options, "context", {})
        }
        m[id].timespan.mark()
        timer.observeFieldScoped("fire", (sub(event as object)
            id = event.getRoSgNode().id
            container = m[id]
            callback = container.callback
            callback(container.context)
            container.timer.unobserveFieldScoped("fire")
            m.delete(id)
        end sub).toStr().mid(10))
    end if
    if m[id] &lt;> invalid then
        maxWait = get(options, "maxWait", -1)
        if maxWait > 0 and m[id].timespan.TotalMilliseconds() >= maxWait then
            m[id].timespan.mark()
            container = m[id]
            callback = container.callback
            callback(container.context)
            container.timer.unobserveFieldScoped("fire")
            m.delete(id)
        else
            timer = m[id].timer
            timer.duration = duration
            timer.control = "start"
        end if
    end if
end sub
' * Invokes sub after wait milliseconds. Any additional arguments are provided to subwhen it's invoked.
' * @since 0.0.22
' * @category Function
' * @param {Sub} callback - The sub to be called after a set delay
' * @param {Float} [wait] - The number of milliseconds to delay invocation
' * @param {Dynamic} context - a single item of data to be passed into the callback when invoked
sub delay(callback as Function, wait = 0 as float, context = invalid as dynamic)
    duration = bslib_ternary(wait = 0, 0.0001, wait / 1000)
    timer = createObject("RoSGNode", "Timer")
    timer.update({
        duration: duration
        repeat: false
        id: "__delay_" + createObject("roDeviceInfo").getRandomUUID()
    })
    m[timer.id] = {
        timer: timer
        callback: callback
        context: context
    }
    timer.observeFieldScoped("fire", (sub(event as object)
        delayId = event.getNode()
        options = m[delayId]
        callback = options.callback
        try
            callback(options.context)
        catch e
            print "Crash during utils.delay:"
            print e
        end try
        m[delayId].timer.unobserveFieldScoped("fire")
        m.delete(delayId)
    end sub).toStr().mid(10))
    timer.control = "start"
end sub
' * Creates an array of array values not included in the other given arrays using SameValueZero for equality comparisons. The order and references of result values are determined by the first array.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to inspect
' * @param {Array} values - The values to exclude
' * @returns {Array} difference - Returns the new array of filtered values
function difference(array = [] as object, values = [] as object) as object
    return differenceBy(array, values)
end function

' * This method is like rodash.difference except that it accepts iteratee which is invoked for each element of array and values to generate the criterion by which they're compared. The order and references of result values are determined by the first array. The iteratee is invoked with one argument:(value).
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to inspect
' * @param {Array} values - The values to exclude
' * @param {Dynamic} iteratee - The iteratee invoked per element
' * @returns {Array} returnArray - Returns the new array of filtered values
function differenceBy(array = [] as object, values = [] as object, iteratee = invalid) as object
    iterateeIsFunction = isFunction(iteratee)
    iterateeIsProperty = NOT iterateeIsFunction AND isString(iteratee)
    returnArray = []
    for each item in array
        convertedItem = item
        if iterateeIsFunction then
            convertedItem = iteratee(item)
        else if iterateeIsProperty then
            convertedItem = item[iteratee]
        end if
        found = false
        for each valueToMatch in values
            if iterateeIsFunction then
                valueToMatch = iteratee(valueToMatch)
            else if iterateeIsProperty
                valueToMatch = valueToMatch[iteratee]
            end if
            if convertedItem = valueToMatch then
                found = true
                exit for
            end if
        end for
        if NOT found then
            returnArray.push(item)
        end if
    end for
    return returnArray
end function

' * This method is like rodash.difference except that it accepts comparator which is invoked to compare elements of array to values. The order and references of result values are determined by the first array. The comparator is invoked with two arguments: (arrVal, othVal).
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to inspect
' * @param {Array} values - The values to exclude
' * @param {Dynamic} iteratee - The iteratee invoked per element
' * @returns {Array} returnArray - Returns the new array of filtered values
function differenceWith(array = [] as object, values = [] as object, comparator = invalid) as object
    returnArray = []
    if isFunction(comparator) then
        for i = 0 to array.count() - 1
            itemOne = array[0]
            itemTwo = array[1]
            if isNotInvalid(itemOne) AND isNotInvalid(itemTwo) AND NOT isEqual(itemOne, itemTwo) then
                returnArray.push(itemOne)
            end if
        end for
    end if
    return returnArray
end function

' * Divides two numbers
' * @since 0.0.21
' * @category Math
' * @param {Dynamic} dividend - The first number in a division
' * @param {Dynamic} divisor - The second number in a division
' * @returns {Integer} value - Returns the quotient
function divide(dividend = 0 as dynamic, divisor = 0 as dynamic) as dynamic
    if isNaN(dividend) OR isNaN(divisor)
        return 0
    end if
    if lte(divisor, 0) then
        return dividend
    end if
    return dividend / divisor
end function

' * Creates a slice of array with n elements dropped from the beginning.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to query
' * @param {Integer} n - The number of elements to drop
' * @returns {Array} array - Returns the slice of array
function drop(array = {} as object, n = 1 as integer)
    array = clone(array)
    for i = 0 to n - 1
        array.shift()
        if array.count() = 0 then
            exit for
        end if
    end for
    return array
end function

' * Creates a slice of array with n elements dropped from the end.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to query
' * @param {Integer} n - The number of elements to drop
' * @returns {Array} array - Returns the slice of array
function dropRight(array = {} as object, n = 1 as integer)
    array = clone(array)
    array.reverse()
    array = drop(array, n)
    array.reverse()
    return array
end function

' * Creates a slice of array excluding elements dropped from the end. Elements are dropped until predicate returns falsey. The predicate is invoked with three arguments: (value, index, array).
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to query
' * @param {Dynamic} predicate - The function invoked per iteration
' * @returns {Array} array - Returns the slice of array
function dropRightWhile(array = {} as object, predicate = invalid)
    array = clone(array)
    array.reverse()
    array = dropWhile(array, predicate)
    array.reverse()
    return array
end function

' * Creates a slice of array excluding elements dropped from the beginning. Elements are dropped until predicate returns falsey. The predicate is invoked with three arguments: (value, index, array).
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to query
' * @param {Dynamic} predicate - The function invoked per iteration
' * @return {Array} array - Returns the slice of array
function dropWhile(array = [] as object, predicate = invalid)
    array = clone(array)
    for i = 0 to array.count() - 1
        item = array[i]
        if isFunction(predicate) then
            if NOT predicate(item) then
                return slice(array, i)
            end if
        else if isAA(predicate) then
            if NOT isEqual(item, predicate) then
                return slice(array, i)
            end if
        else if isArray(predicate) then
            if NOT isEqual(item[predicate[0]], predicate[1]) then
                return slice(array, i)
            end if
        else if isString(predicate) then
            if NOT item[predicate] then
                return slice(array, i)
            end if
        end if
    end for
    return array
end function

' * Checks if `string` ends with the given target string.
' * @since 0.0.21
' * @category String
' * @param {String} source - The string to search.
' * @param {String} target - The string to search for.
' * @param {Number} position - The position to search up to.
' * @returns {Boolean} - Returns `true` if `string` ends with `target`, else `false`.
function endsWith(source = "" as string, target = "" as string, position = invalid as dynamic)
    if isInvalid(position) then
        position = source.len()
    end if
    return source.endsWith(target, position)
end function

' * Checks if two values are equivalent.
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value - The value to compare.
' * @param {Dynamic} other - The other value to compare.
' * @returns {Boolean} - Returns `true` if the values are equivalent, else `false`.
function eq(value as dynamic, other as dynamic)
    return isEqual(value, other)
end function

' * Escapes a string for insertion into HTML, replacing &amp;, &lt;, >, ", `, and ' characters.
' * @since 0.0.21
' * @category String
' * @param {String} source - The string to escape.
' * @returns {String} - The escaped string.
function escape(source = "" as string)
    return source.escape()
end function

' * Escapes a string for insertion into a regular expression.
' * @since 0.0.21
' * @category String
' * @param {String} source - The string to escape.
' * @returns {String} - The escaped string.
function escapeRegExp(source = "" as string)
    replaceArray = [
        "^"
        "$"
        ""
        "."
        "*"
        "+"
        "?"
        "("
        ")"
        "["
        "]"
        "{"
        "}"
        "|"
    ]
    for each char in replaceArray
        source = source.replace(char, "\" + char)
    end for
    return source.replace("\/\/", "//")
end function

' * Fills elements of array with value from start up to, but not including, end.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to fill
' * @param {Dynamic} value - The value to fill array with
' * @param {Integer} startPos - The start position
' * @param {Integer} endPos - The end position
' * @returns {Array} array - Returns the mutated array
function fill(array = [] as object, value = "" as dynamic, startPos = invalid, endPos = invalid)
    if isInvalid(startPos) then
        startPos = 0
    end if
    if isInvalid(endPos) then
        endPos = array.count()
    end if
    endPos = endPos - 1
    for i = startPos to endPos
        array[i] = value
    end for
    return array
end function

' * Iterates over elements of collection, returning the first element predicate returns truthy for. The predicate is invoked with three arguments: (value, index|key, collection).
' * @since 0.0.22
' * @category Collection
' * @param {Array} array - The array to inspect
' * @param {Dynamic} predicate - The function invoked per iteration
' * @param {Integer} fromIndex - The index to search from
' * @returns {Dymanic} match - Returns the matched element, else invalid.
function find(array, predicate = invalid as dynamic, fromIndex = 0 as integer) as dynamic
    if not isArray(array) then
        return invalid
    end if
    foundIndex = findIndex(array, predicate, fromIndex)
    if foundIndex = -1 then
        return invalid
    end if
    return array[foundIndex]
end function

' * This method is like rodash.find except that it returns the index of the first element predicate returns truthy for instead of the element itself.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to inspect
' * @param {Dynamic} predicate - The function invoked per iteration
' * @param {Integer} fromIndex - The index to search from
' * @returns {Integer} index - Returns the index of the found element, else -1
function findIndex(array, predicate = invalid as dynamic, fromIndex = 0 as integer) as integer
    for index = fromIndex to array.count() - 1
        item = array[index]
        if isFunction(predicate) then
            if predicate(item) then
                return index
            end if
        else if isAA(predicate) then
            if isEqual(item, predicate) then
                return index
            end if
        else if isArray(predicate) then
            if isEqual(item[predicate[0]], predicate[1]) then
                return index
            end if
        else if isString(predicate) and item.doesExist(predicate) then
            if item[predicate] then
                return index
            end if
        end if
    end for
    return -1
end function

' * This method is like rodash.findIndex except that it iterates over elements of collection from right to left.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to inspect
' * @param {Dynamic} predicate - The function invoked per iteration
' * @param {Integer} fromIndex - The index to search from
' * @returns {Integer} index - Returns the index of the found element, else -1
function findLastIndex(array, predicate = invalid, fromIndex = 0 as integer)
    array = clone(array)
    array.reverse()
    foundIndex = findIndex(array, predicate, fromIndex)
    if foundIndex = -1 then
        return -1
    end if
    return array.count() - 1 - foundIndex
end function

' * An alias to the head function.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to query
' * @return {Dynamic} Returns the first element of array
function first(array = [])
    return head(array)
end function

' * Flattens array a single level deep.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to flatten
' * @returns {Dynamic} Returns the new flattened array
function flatten(array = [])
    returnArray = []
    for each item in array
        if type(item) = "roArray" then
            returnArray.append(item)
        else
            returnArray.push(item)
        end if
    end for
    return returnArray
end function

' * Recursively flattens array.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to flatten
' * @returns {Dynamic} Returns the new flattened array
function flattenDeep(array = [])
    returnArray = []
    for each item in array
        if type(item) = "roArray" then
            returnArray.append(flattenDeep(item))
        else
            returnArray.push(item)
        end if
    end for
    return returnArray
end function

' * Recursively flatten array up to depth times.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to flatten
' * @param {Integer} depth - The maximum recursion depth
' * @return {Dynamic} Returns the new flattened array
function flattenDepth(array = invalid, depth = 1 as integer)
    returnArray = []
    if NOT isArray(array) then
        return array
    end if
    for i = 0 to array.count() - 1
        item = array[i]
        if (depth > 1) then
            item = flattenDepth(item, depth - 1)
            if isArray(item) then
                returnArray.append(item)
            else
                returnArray.push(item)
            end if
        else
            if isArray(item) then
                returnArray.append(item)
            else
                returnArray.push(item)
            end if
        end if
    end for
    return returnArray
end function

' * Computes number rounded down to precision
' * @since 0.0.21
' * @category Math
' * @param {Integer} number - The number to round down
' * @param {Integer} precision - The precision to round down to
' * @return {Integer} Returns the rounded down number
function floor(number = 0, precision = 0 as dynamic) as dynamic
    return abs(int(number * 10 ^ precision)) / 10 ^ precision
end function

' * Iterates over elements of collection and invokes iteratee for each element. The iteratee is invoked with three arguments: (value, index|key, collection). Iteratee functions may exit iteration early by explicitly returning false.
' * Note: As with other "Collections" methods, objects with a "length" property are iterated like arrays. To avoid this behavior use rodash.forIn or rodash.forOwn for object iteration.
' * @since 0.0.21
' * @category Collection
' * @param {Dynamic} collection - The collection to iterate over
' * @param {Dynamic} iteratee - The function invoked per iteration
' * @return {Dynamic} Returns collection
function forEach(collection = invalid as dynamic, iteratee = invalid as dynamic)
    return internal_baseForEach(collection, iteratee)
end function

' * This method is like rodash.forEach except that it iterates over elements of collection from right to left.
' * @since 0.0.21
' * @category Collection
' * @param {Dynamic} collection - The collection to iterate over
' * @param {Dynamic} iteratee - The function invoked per iteration
' * @return {Dynamic} Returns collection
function forEachRight(collection = invalid as dynamic, iteratee = invalid as dynamic)
    return internal_baseForEach(collection, iteratee, "right")
end function

' * Iterates over own and inherited enumerable string keyed properties of an object and invokes iteratee for each property. The iteratee is invoked with three arguments: (value, key, object). Iteratee functions may exit iteration early by explicitly returning false.
' * @since 0.0.21
' * @category Object
' * @param {Dynamic} obj - The object to iterate over
' * @param {Dynamic} iteratee - The function invoked per iteration
' * @return {Object} Returns object
function forIn(obj = {} as object, iteratee = invalid as dynamic)
    return internal_baseForEach(obj, iteratee, "left", "omit")
end function

' * This method is like rodash.forIn except that it iterates over properties of object in the opposite order.
' * @since 0.0.21
' * @category Object
' * @param {Dynamic} obj - The object to iterate over
' * @param {Dynamic} iteratee - The function invoked per iteration
' * @return {Object} Returns object
function forInRight(obj = {} as object, iteratee = invalid as dynamic)
    return internal_baseForEach(obj, iteratee, "right", "omit")
end function

' * Iterates over own enumerable string keyed properties of an object and invokes iteratee for each property. The iteratee is invoked with three arguments: (value, key, object). Iteratee functions may exit iteration early by explicitly returning false.
' * @since 0.0.21
' * @category Object
' * @param {Dynamic} obj - The object to iterate over
' * @param {Dynamic} iteratee - The function invoked per iteration
' * @return {Object} Returns object
function forOwn(obj = {} as object, iteratee = invalid as dynamic)
    return internal_baseForEach(obj, iteratee, "left", "omit")
end function

' * This method is like rodash.forOwn except that it iterates over properties of object in the opposite order.
' * @since 0.0.21
' * @category Object
' * @param {Dynamic} obj - The object to iterate over
' * @param {Dynamic} iteratee - The function invoked per iteration
' * @return {Object} Returns object
function forOwnRight(obj = {} as object, iteratee = invalid as dynamic)
    return internal_baseForEach(obj, iteratee, "right", "omit")
end function

' * Converts an ISO 8601 string to a date object.
' * @since 0.0.21
' * @category Date
' * @param {String} dateString - The date string to convert.
' * @returns {Object} - The date objects.
function fromISO8601String(dateString = "" as string) as object
    dateObj = internal_getDateObject()
    return {
        "utc": dateObj.utc.fromISO8601String(dateString)
        "local": dateObj.local.fromISO8601String(dateString)
    }
end function

' * The inverse of rodash.toPairs; this method returns an object composed from key-value pairs.
' * @since 0.0.24
' * @category Array
' * @param {Array} pairs - And array of arrays to be converted to an object
' * @return {Object} Returns the new object
function fromPairs(pairs = [] as object) as object
    returnObject = {}
    if not isArray(pairs) then
        return returnObject
    end if
    for each pair in pairs
        if isArray(pair) and pair.count() = 2 then
            returnObject[pair[0]] = pair[1]
        end if
    end for
    return returnObject
end function

' * Converts a number of seconds to a date object.
' * @since 0.0.21
' * @category Date
' * @param {Number} numSeconds - The number of seconds to convert.
' * @returns {Object} - The date objects.
function fromSeconds(numSeconds = 0 as integer) as object
    dateObj = internal_getDateObject()
    return {
        "utc": dateObj.utc.fromSeconds(numSeconds)
        "local": dateObj.local.fromSeconds(numSeconds)
    }
end function

' TODO: Rewrite this due to scoping issue
' * @ignore
' * Creates an array of function property names from own enumerable properties of object.
' * @category Object
' * @param {Dynamic} obj - The object to iterate over
' * @return {Object} Returns object
function functions(obj = {} as object)
    ' return rodash.base.forEach.baseForEach(obj, invalid, "left", "only")
    return []
end function

' TODO: Rewrite this due to scoping issue
' * @ignore
' * Creates an array of function property names from own and inherited enumerable properties of object.
' * @param {Dynamic} obj - The object to iterate over
' * @return {Object} Returns object
function functionsIn(obj = {} as object)
    ' return rodash.base.forEach.baseForEach(obj, invalid, "left", "only")
    return []
end function

' * Gets the value at path of object. If the resolved value is undefined, the defaultValue is returned in its place.
' * @since 0.0.21
' * @category Object
' * @param {Object} aa - Object to drill down into.
' * @param {String} keyPath - A dot notation based string to the expected value.
' * @param {Dynamic} fallback - A return fallback value if the requested field could not be found or did not pass the validator function.
' * @param {Function} validator - A function used to validate the output value matches what you expected.
' * @return {Dynamic} The result of the drill down process
function get(aa as object, keyPath as string, fallback = Invalid as dynamic, validator = isNotInvalid as Function) as dynamic
    if NOT (internal_isKeyedValueType(aa) OR isNonEmptyArray(aa)) OR keyPath = "" then
        return fallback
    end if
    nextValue = aa
    keyPath = internal_sanitizeKeyPath(keyPath)
    keys = keyPath.tokenize(".").toArray()
    startingKeys = join(keys, " - ")
    for each key in keys
        if internal_isKeyedValueType(nextValue) then
            nextValue = nextValue[key]
        else if isNonEmptyArray(nextValue) then
            nextValue = nextValue[toNumber(key)]
        else
            return fallback
        end if
    end for
    if NOT validator(nextValue) then
        return fallback
    end if
    return nextValue
end function

' * Gets the AA value at path of object. Calls rodash.get with the isAA validator function.
' * @since 0.0.25
' * @category Object
' * @param {Object} aa - Object to drill down into.
' * @param {String} keyPath - A dot notation based string to the expected value.
' * @param {Assocarray} fallback - A return fallback value if the requested field could not be found or did not pass the validator function.
' * @return {Assocarray} The result of the drill down process
function getAA(aa as object, keyPath as string, fallback = {} as object) as object
    return get(aa, keyPath, fallback, isAA)
end function

' * Gets the Array value at path of object. Calls rodash.get with the isArray validator function.
' * @since 0.0.25
' * @category Object
' * @param {Object} aa - Object to drill down into.
' * @param {String} keyPath - A dot notation based string to the expected value.
' * @param {Array} fallback - A return fallback value if the requested field could not be found or did not pass the validator function.
' * @return {Array} The result of the drill down process
function getArray(aa as object, keyPath as string, fallback = [] as object) as object
    return get(aa, keyPath, fallback, isArray)
end function

' * Gets the boolean value at path of object. Calls rodash.get with the isBoolean validator function.
' * @since 0.0.25
' * @category Object
' * @param {Object} aa - Object to drill down into.
' * @param {String} keyPath - A dot notation based string to the expected value.
' * @param {Boolean} fallback - A return fallback value if the requested field could not be found or did not pass the validator function.
' * @return {Boolean} The result of the drill down process
function getBoolean(aa as object, keyPath as string, fallback = false as boolean) as boolean
    return get(aa, keyPath, fallback, isBoolean)
end function

' * Gets the day of the month.
' * @since 0.0.21
' * @category Date
' * @returns {Object} - The day of the month.
function getDayOfMonth() as object
    dateObj = internal_getDateObject()
    return {
        "utc": dateObj.utc.getDayOfMonth()
        "local": dateObj.local.getDayOfMonth()
    }
end function

' * Gets the day of the week.
' * @since 0.0.21
' * @category Date
' * @returns {Object} - The day of the week.
function getDayOfWeek() as object
    dateObj = internal_getDateObject()
    return {
        "utc": dateObj.utc.getDayOfWeek()
        "local": dateObj.local.getDayOfWeek()
    }
end function

' * Gets the hours.
' * @since 0.0.21
' * @category Date
' * @returns {Object} - The hours.
function getHours() as object
    dateObj = internal_getDateObject()
    return {
        "utc": dateObj.utc.getHours()
        "local": dateObj.local.getHours()
    }
end function

' * Gets the last day of the month.
' * @since 0.0.21
' * @category Date
' * @returns {Object} - The last day of the month.
function getLastDayOfMonth() as object
    dateObj = internal_getDateObject()
    return {
        "utc": dateObj.utc.getLastDayOfMonth()
        "local": dateObj.local.getLastDayOfMonth()
    }
end function

' * Gets the milliseconds.
' * @since 0.0.21
' * @category Date
' * @returns {Object} - The milliseconds.
function getMilliseconds() as object
    dateObj = internal_getDateObject()
    return {
        "utc": dateObj.utc.getMilliseconds()
        "local": dateObj.local.getMilliseconds()
    }
end function

' * Gets the minutes.
' * @since 0.0.21
' * @category Date
' * @returns {Object} - The minutes.
function getMinutes() as object
    dateObj = internal_getDateObject()
    return {
        "utc": dateObj.utc.getMinutes()
        "local": dateObj.local.getMinutes()
    }
end function

' * Gets the month.
' * @since 0.0.21
' * @category Date
' * @returns {Object} - The month.
function getMonth() as object
    dateObj = internal_getDateObject()
    return {
        "utc": dateObj.utc.getMonth()
        "local": dateObj.local.getMonth()
    }
end function

' * Gets the number value at path of object. Calls rodash.get with the isNumber validator function.
' * @since 0.0.25
' * @category Object
' * @param {Object} aa - Object to drill down into.
' * @param {String} keyPath - A dot notation based string to the expected value.
' * @param {Number} fallback - A return fallback value if the requested field could not be found or did not pass the validator function.
' * @return {Number} The result of the drill down process
function getNumber(aa as object, keyPath as string, fallback = -1 as dynamic) as dynamic
    return get(aa, keyPath, fallback, isNumber)
end function

' * Gets the seconds.
' * @since 0.0.21
' * @category Date
' * @returns {Object} - The seconds.
function getSeconds() as object
    dateObj = internal_getDateObject()
    return {
        "utc": dateObj.utc.getSeconds()
        "local": dateObj.local.getSeconds()
    }
end function

' * Gets the String value at path of object. Calls rodash.get with the isString validator function.
' * @since 0.0.25
' * @category Object
' * @param {Object} aa - Object to drill down into.
' * @param {String} keyPath - A dot notation based string to the expected value.
' * @param {String} fallback - A return fallback value if the requested field could not be found or did not pass the validator function.
' * @return {String} The result of the drill down process
function getString(aa as object, keyPath as string, fallback = "" as string) as string
    return get(aa, keyPath, fallback, isString)
end function

' * Gets the year.
' * @since 0.0.21
' * @category Date
' * @returns {Object} - The year.
function getYear() as object
    dateObj = internal_getDateObject()
    return {
        "utc": dateObj.utc.getYear()
        "local": dateObj.local.getYear()
    }
end function

' * Creates an object composed of keys generated from the results of running each element of collection thru iteratee. The order of grouped values is determined by the order they occur in collection. The corresponding value of each key is an array of elements responsible for generating the key. The iteratee is invoked with one argument: (value).
' * @since 0.0.23
' * @category Collection
' * @param {Object} collection - The collection to iterate over.
' * @param {Function|String} iteratee - The iteratee to transform keys.
' * @returns {Object} - Returns the composed aggregate object.
function groupBy(collection as Object, iteratee = invalid as dynamic) as Object
    if not isArray(collection) then
        return {}
    end if
    collection = clone(collection)
    acc = {}
    if isFunction(iteratee) then
        for each value in collection
            key = toString(iteratee(value))
            if acc.doesExist(key)
                acc[key].push(value)
            else
                acc[key] = [
                    value
                ]
            end if
        end for
    else if isString(iteratee) then
        for each value in collection
            if isAA(value) and value.doesExist(iteratee) then
                key = toString(value[iteratee])
                if acc.doesExist(key)
                    acc[key].push(value)
                else
                    acc[key] = [
                        value
                    ]
                end if
            end if
        end for
    end if
    return acc
end function

' * Checks if value is greater than other.
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value - The value to compare.
' * @param {Dynamic} other - The other value to compare.
' * @returns {Boolean} - Returns `true` if value is greater than other, else `false`.
function gt(value as dynamic, other as dynamic)
    return value > other
end function

' * Checks if value is greater than or equal to other.
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value - The value to compare.
' * @param {Dynamic} other - The other value to compare.
' * @returns {Boolean} - Returns `true` if value is greater than or equal to other, else `false`.
function gte(value as dynamic, other as dynamic)
    return value >= other
end function

' * Checks if first level of the supplied AssociativeArray contains the Array of key strings.
' * @since 0.0.21
' * @category Object
' * @param {Dynamic} aaValue - AssociativeArray to be checked
' * @return {Dynamic} keys - Array of key strings
function hasKeys(aaValue as dynamic, keys as dynamic) as boolean
    if NOT internal_isKeyedValueType(aaValue) OR aaValue.isEmpty() OR NOT isArray(keys) OR keys.isEmpty() then
        return false
    end if
    hasKeys = true
    for each key in keys
        if NOT aaValue.doesExist(key) then
            hasKeys = false
            exit for
        end if
    end for
    return hasKeys
end function

' * Gets the first element of array.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to query
' * @return {Dynamic} Returns the first element of array
function head(array = [] as object) as dynamic
    return array[0]
end function

' * Checks if number is between start and up to, but not including, end. If end is not specified, it's set to start with start then set to 0.
' * @since 0.0.21
' * @category Number
' * @param {Number} number - The number to check.
' * @param {Number} [startPos=0] - The start of the range.
' * @param {Number} [endPos=startPos] - The end of the range.
' * @returns {Boolean} - Returns `true` if number is in the range, else `false`.
function inRange(number as dynamic, startPos = 0 as dynamic, endPos = invalid as dynamic)
    if (isInvalid(endPos)) then
        endPos = startPos
        startPos = 0
    end if
    if gt(startPos, endPos) then
        startPosTemp = startPos
        endPosTemp = endPos
        startPos = endPosTemp
        endPos = startPosTemp
    end if
    return gte(number, startPos) AND lt(number, endPos)
end function

' * Gets the index at which the first occurrence of value is found in array using SameValueZero for equality comparisons. If fromIndex is negative, it's used as the offset from the end of array.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to inspect
' * @param {Dynamic} value - The value to search for
' * @param {Integer} fromIndex - The index to search from
' * @return {Integer} Returns the index of the matched value, else -1
function indexOf(array = [] as object, value = invalid, fromIndex = invalid)
    if NOT isArray(array) then
        return -1
    end if
    if isInvalid(fromIndex) then
        fromIndex = 0
    end if
    for index = fromIndex to array.count() - 1
        item = array[index]
        if isEqual(item, value) then
            return index
        end if
    end for
    return -1
end function

' * Gets all but the last element of array.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to query
' * @return {Array} Returns the slice of array
function initial(array = [] as object)
    if NOT isArray(array) then
        return []
    end if
    return slice(array, 0, array.count() - 1)
end function

' * @ignore
' * Attempts to convert the supplied value to a string.
' * @since 0.0.21
' * @category Internal
' * @param {Dynamic} value - The value to convert.
' * @return {String} - Results of the conversion.
function internal_aaToString(aa as Object) as String
    description = "{"
    for each key in aa
        description += key + ": " + toString(aa[key]) + ", "
    end for
    description = description.left(description.len() - 2) + "}"
    return description
end function

' * @ignore
' * Attempts to convert the supplied value to a string.
' * @since 0.0.21
' * @category Internal
' * @param {Dynamic} value The value to convert.
' * @return {String} Results of the conversion.
function internal_arrayToString(array as Object) as String
    description = "["
    for each item in array
        description += toString(item) + ", "
    end for
    description = description.left(description.len() - 2) + "]"
    return description
end function

' * @ignore
' * The base implementation of `forEach`.
' * @since 0.0.21
' * @category Internal
' * @param {Array|Object} - collection The collection to iterate over
' * @param {Function} iteratee The function invoked per iteration
' * @param {String} direction - the direction to traverse the collection
' * @param {String} funcValueRule - Filters functions from collection. `allow`, `omit`, `only`.
' * @return {Array|Object} Returns `collection`
function internal_baseForEach(collection = invalid as dynamic, iteratee = invalid as dynamic, direction = "left", funcValueRule = "allow" as string)
    if isInvalid(collection) OR isEmpty(collection) then
        return invalid
    end if
    isRight = direction = "right"
    if isAA(collection) then
        keys = collection.keys()
        if isRight then
            keys.reverse()
        end if
        for each key in keys
            item = collection[key]
            if isFunction(iteratee) then
                valueIsFunction = isFunction(item)
                allowValue = false
                if valueIsFunction AND NOT isEqual(funcValueRule, "omit")
                    allowValue = true
                else if NOT valueIsFunction AND NOT isEqual(funcValueRule, "only")
                    allowValue = true
                end if
                if allowValue then
                    iteratee(item, key)
                end if
            end if
        end for
    else
        if isRight then
            collection.reverse()
        end if
        for each item in collection
            if isFunction(iteratee) then
                iteratee(item)
            end if
        end for
        if isRight then
            collection.reverse()
        end if
    end if
    return collection
end function

' * @ignore
' * Attempts to convert the supplied value to a string.
' * @since 0.0.21
' * @category Internal
' * @param {Dynamic} value The value to convert.
' * @return {String} Results of the conversion.
function internal_booleanToString(bool as Boolean) as String
    if bool then
        return "true"
    end if
    return "false"
end function

' * @ignore
' * Checks if the supplied values can be compared in a if statement.
' * @since 0.0.21
' * @category Internal
' * @param {Dynamic} valueOne - First value
' * @param {Dynamic} valueTwo - Second value
' * @return {Boolean} True if the values can be compared in a if statement
function internal_canBeCompared(valueOne as Dynamic, valueTwo as Dynamic) as Boolean
    ' If the first argument is true we don't need to check the following conditionals
    if isString(valueOne) then
        if isString(valueTwo) then
            return true
        end if
    else if isNumber(valueOne) then
        if isNumber(valueTwo) then
            return true
        end if
    else if isBoolean(valueOne) then
        if isBoolean(valueTwo) then
            return true
        end if
    else if isInvalid(valueOne) then
        if isInvalid(valueTwo) then
            return true
        end if
    end if
    return false
end function

' * @ignore
function internal_getConstants()
    return {
        BrightScriptErrorCodes: {
            ' runtime errors
            ERR_OKAY: &amp;hFF
            ERR_NORMAL_END: &amp;hFC ' normal, but terminate execution.  END, shell "exit", window closed, etc.
            ERR_VALUE_RETURN: &amp;hE2 ' return executed, and a value returned on the stack
            ERR_INTERNAL: &amp;hFE ' A condition that shouldn't occur did
            ERR_UNDEFINED_OPCD: &amp;hFD ' A opcode that we don't handle
            ERR_UNDEFINED_OP: &amp;hFB ' An expression operator that we don't handle
            ERR_MISSING_PARN: &amp;hFA
            ERR_STACK_UNDER: &amp;hF9 ' nothing on stack to pop
            ERR_BREAK: &amp;hF8 ' scriptBreak() called
            ERR_STOP: &amp;hF7 ' stop statement executed
            ERR_RO0: &amp;hF6 ' bscNewComponent failed because object class not found
            ERR_RO1: &amp;hF5 ' ro function call does not have the right number of parameters
            ERR_RO2: &amp;hF4 ' member function not found in object or interface
            ERR_RO3: &amp;hF3 ' Interface not a member of object
            ERR_TOO_MANY_PARAM: &amp;hF2 ' Too many function parameters to handle
            ERR_WRONG_NUM_PARAM: &amp;hF1 ' Incorect number of function parameters
            ERR_RVIG: &amp;hF0 ' Function returns a value, but is ignored
            ERR_NOTPRINTABLE: &amp;hEF ' Non Printable value
            ERR_NOTWAITABLE: &amp;hEE ' Tried to Wait on a function that does not have MessagePort interface
            ERR_MUST_BE_STATIC: &amp;hED ' interface calls from type rotINTERFACE must by static
            ERR_RO4: &amp;hEC ' . operator used on a variable that does not contain a legal object or interface reference
            ERR_NOTYPEOP: &amp;hEB ' operation on two typless operands attempted
            ERR_USE_OF_UNINIT_VAR: &amp;hE9 ' illegal use of uninited var
            ERR_TM2: &amp;hE8 ' non-numeric index to array
            ERR_ARRAYNOTDIMMED: &amp;hE7
            ERR_USE_OF_UNINIT_BRSUBREF: &amp;hE6 ' used a reference to SUB that is not initilized
            ERR_MUST_HAVE_RETURN: &amp;hE5
            ERR_INVALID_LVALUE: &amp;hE4 ' invalid left side of expression
            ERR_INVALID_NUM_ARRAY_IDX: &amp;hE3 ' invalid number of array indexes
            ERR_UNICODE_NOT_SUPPORTED: &amp;hE1
            ERR_NOTFUNOPABLE: &amp;hE0
            ERR_STACK_OVERFLOW: &amp;hDF
            ERR_THROWN_EXCEPTION_ON_STACK: &amp;hDE '(Internal use only)
            ERR_SYNTAX: &amp;h02
            ERR_DIV_ZERO: &amp;h14
            ERR_MISSING_LN: &amp;h0E
            ERR_OUTOFMEM: &amp;h0C
            ERR_STRINGTOLONG: &amp;h1C
            ERR_TM: &amp;h18 ' Type Mismatch (string / numeric operation mismatch)
            ERR_OS: &amp;h1A ' out of string space
            ERR_RG: &amp;h04 ' Return without Gosub
            ERR_NF: &amp;h00 ' Next without For
            ERR_FC: &amp;h08 ' Invalid parameter passed to function/array (e.g neg matrix dim or squr root)
            ERR_DD: &amp;h12 ' Attempted to redimension an array
            ERR_BS: &amp;h10 ' Array subscript out of bounds
            ERR_OD: &amp;h06 ' Out of Data (READ)
            ERR_CN: &amp;h20 ' Continue Not Allowed
            ERR_BITSHIFT_BAD: &amp;h1E ' Invalid Bitwise Shift
            ERR_EXECUTION_TIMEOUT: &amp;h23 ' Timeout on critical thread
            ERR_CONSTANT_OVERFLOW: &amp;h22 ' Constant Out Of Range
            ERR_FORMAT_SPECIFIER: &amp;h24 ' Invalid Format Specifier
            ERR_BAD_THROW: &amp;h26 ' Invalid argument to Throw
            ERR_USER: &amp;h28 ' User-specified exception
            ' compiler errors
            ERR_NW: &amp;hBF ' EndWhile with no While
            ERR_MISSING_ENDWHILE: &amp;hBE ' While Statement is missing a matching EndWhile
            ERR_MISSING_ENDIF: &amp;hBC ' end of code reached without finding ENDIF
            ERR_NOLN: &amp;hBB ' no line number found
            ERR_LNSEQ: &amp;hBA ' Line number sequence error
            ERR_LOADFILE: &amp;hB9 ' Error loading a file
            ERR_NOMATCH: &amp;hB8 ' "Match" statement did not match
            ERR_UNEXPECTED_EOF: &amp;hB7 ' End of string being compiled encountered when not expected (missing end of block usually)
            ERR_FOR_NEXT_MISMATCH: &amp;hB6 ' Variable on a NEXT does not match that for the FOR
            ERR_NO_BLOCK_END: &amp;hB5
            ERR_LABELTWICE: &amp;hB4 ' Label defined more than once
            ERR_UNTERMED_STRING: &amp;hB3 ' litteral string does not have ending quote
            ERR_FUN_NOT_EXPECTED: &amp;hB2
            ERR_TOO_MANY_CONST: &amp;hB1
            ERR_TOO_MANY_VAR: &amp;hB0
            ERR_EXIT_WHILE_NOT_IN_WHILE: &amp;hAF
            ERR_INTERNAL_LIMIT_EXCEDED: &amp;hAE
            ERR_SUB_DEFINED_TWICE: &amp;hAD
            ERR_NOMAIN: &amp;hAC
            ERR_FOREACH_INDEX_TM: &amp;hAB
            ERR_RET_CANNOT_HAVE_VALUE: &amp;hAA
            ERR_RET_MUST_HAVE_VALUE: &amp;hA9
            ERR_FUN_MUST_HAVE_RET_TYPE: &amp;hA8
            ERR_INVALID_TYPE: &amp;hA7
            ERR_NOLONGER: &amp;hA6 ' no longer supported
            ERR_EXIT_FOR_NOT_IN_FOR: &amp;hA5
            ERR_MISSING_INITILIZER: &amp;hA4
            ERR_IF_TOO_LARGE: &amp;hA3
            ERR_RO_NOT_FOUND: &amp;hA2
            ERR_TOO_MANY_LABELS: &amp;hA1
            ERR_VAR_CANNOT_BE_SUBNAME: &amp;hA0
            ERR_INVALID_CONST_NAME: &amp;h9F
            ERR_CONST_FOLDING: &amp;h9E
            ERR_BUILTIN_FUNCTION: &amp;h9D
            ERR_FUNCTION_NOT_IN_NAMESPACE: &amp;h91
            ERR_EVAL_UNSUPPORTED: &amp;h90
            ERR_LABEL_INSIDE_TRY: &amp;h8F
        }
        MAX_INT: 2147483647
        MIN_INT: -2147483648
    }
end function

' * @ignore
function internal_getDateObject() as object
    dateObj = CreateObject("roDateTime")
    utc = dateObj
    dateObj.toLocalTime()
    local = dateObj
    return {
        "utc": utc
        "local": local
    }
end function

' * @ignore
' * Checks if the supplied value allows for key field access
' * @since 0.0.21
' * @category Internal
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function internal_isKeyedValueType(value as Dynamic) as Boolean
    return getInterface(value, "ifAssociativeArray") &lt;> Invalid
end function

' * @ignore
' * Attempts to converts a nodes top level fields to an AssociativeArray.
' * @since 0.0.21
' * @category Internal
' * @param {Dynamic} value - The variable to be converted.
' * @param {Boolean} removeId - If set to true the nodes ID will also be stripped.
' * @param {Object} removeFields - List of keys that need to be removed from the node.
' * @return {Dynamic} Results of the conversion.
function internal_nodeToAA(value as Object, removeId = false as Boolean, removeFields = [] as Object) as Object
    if isNode(value) then
        fields = value.getFields()
        fields.delete("change")
        fields.delete("focusable")
        fields.delete("focusedChild")
        fields.delete("ready")
        if removeId then
            fields.delete("id")
        end if
        'Looping through any additional fields if passed.
        if isNonEmptyArray(removeFields) then
            for each field in removeFields
                fields.delete(field)
            end for
        end if
        return fields
    else if isAA(value) then
        return value
    end if
    return {}
end function

' * @ignore
' * Attempts to convert the supplied value to a string.
' * @since 0.0.21
' * @category Internal
' * @param {Dynamic} value The value to convert.
' * @return {String} Results of the conversion.
function internal_nodeToString(node as Object) as String
    if NOT isNode(node) then
        return ""
    end if
    description = node.subtype()
    if node.isSubtype("Group") then
        ' accessing properties from anywhere but the render thread is too expensive to include here
        id = node.id
        if id &lt;> "" then
            description += " (" + id + ")" + internal_aaToString(internal_nodeToAA(node))
        end if
    end if
    return description
end function

' * @ignore
' * Attempts to convert the supplied value to a string.
' * @param {Dynamic} value The value to convert.
' * @return {String} Results of the conversion.
function internal_numberToString(value as Dynamic) as String
    return value.toStr()
end function

' * @ignore
function internal_orderByCompare(item1 as Object, item2 as Object, keys as Object, orders as Object) as Boolean
    ' Iterate through the keys and corresponding orders
    for i = 0 to keys.Count() - 1
        key = keys[i]
        order = orders[i]
        if order = invalid then
            order = "asc"
        end if
        value1 = item1[key]
        value2 = item2[key]
        ' Compare the values based on the current key
        if value1 &lt;> value2
            if order = "asc"
                return value1 > value2
            else if order = "desc"
                return value1 &lt; value2
            end if
        end if
    end for
    ' If all values are equal, maintain original order
    return false
end function

' * @ignore
function internal_sanitizeKeyPath(value = "" as String)
    regex = createObject("roRegex", "\[(.*?)\]", "i")
    matches = regex.matchAll(value)
    if isNotInvalid(matches) then
        for each match in matches
            if isNotInvalid(match) then
                value = value.replace(match[0], "." + match[1])
            end if
        end for
    end if
    return value
end function

' * Creates an array of unique values that are included in all given arrays using SameValueZero for equality comparisons. The order and references of result values are determined by the first array.
' * @since 0.0.21
' * @category Array
' * @param {Array} mainArray - The main array to inspect
' * @param {Array} inspect - The array to find matches
' * @return {Array} Returns the new array of intersecting values
function intersection(mainArray = [] as object, inspectArray = [] as object) as object
    return intersectionBy(mainArray, inspectArray)
end function

' * This method is like rodash.intersection except that it accepts iteratee which is invoked for each element of each arrays to generate the criterion by which they're compared. The order and references of result values are determined by the first array. The iteratee is invoked with one argument:(value).
' * @since 0.0.21
' * @category Array
' * @param {Array} mainArray - The main array to inspect
' * @param {Array} inspect - The array to find matches
' * @param {Dynamic} iteratee - The iteratee invoked per element
' * @return {Array} Returns the new array of intersecting values
function intersectionBy(mainArray = [] as object, inspectArray = [] as object, iteratee = invalid) as object
    intersectArray = []
    mainArray = clone(mainArray)
    inspectArray = clone(inspectArray)
    if isInvalid(iteratee) then
        if isNonEmptyArray(mainArray) then
            for each item in mainArray
                if NOT isEqual(indexOf(inspectArray, item), - 1) then
                    intersectArray.push(item)
                end if
            end for
        end if
    else if isFunction(iteratee) then
        for i = 0 to inspectArray.count() - 1
            inspectArray[i] = iteratee(inspectArray[i])
        end for
        for each item in mainArray
            if NOT isEqual(indexOf(inspectArray, iteratee(item)), - 1) then
                intersectArray.push(item)
            end if
        end for
    else if isString(iteratee) then
        for each item in mainArray
            findKey = item[iteratee]
            if isNotInvalid(findKey) then
                matchValue = {}
                matchValue[iteratee] = findKey
                if NOT isEqual(findIndex(inspectArray, matchValue), - 1) then
                    intersectArray.push(item)
                end if
            end if
        end for
    end if
    return intersectArray
end function

' * This method is like rodash.intersection except that it accepts comparator which is invoked to compare elements of arrays. The order and references of result values are determined by the first array. The comparator is invoked with two arguments: (arrVal, othVal).
' * @since 0.0.21
' * @category Array
' * @param {Array} mainArray - The main array to inspect
' * @param {Array} inspect - The array to find matches
' * @param {Dynamic} comparator - The comparator invoked per element
' * @return {Array} Returns the new array of intersecting values
function intersectionWith(mainArray = [] as object, inspectArray = [] as object, comparator = invalid) as object
    if NOT isFunction(comparator) then
        return []
    end if
    return intersectionBy(mainArray, inspectArray, comparator)
end function

' * Creates an object composed of the inverted keys and values of `object`.
' * If `object` contains duplicate values, subsequent values overwrite property assignments of previous values.
' * As AssocArrays are sorted, the order of the aa keys is not preserved.
' * @since 0.0.22
' * @category Object
' * @param {Object} object The object to invert.
' * @returns {Object} Returns the new inverted object.
' * @example
' *
' * const object = { 'a': 1, 'b': 2, 'c': 1 }
' * rodash.invert(object)
' * // => { '1': 'c', '2': 'b' }
function invert(originalAA as Object) as Dynamic
    if not isAA(originalAA) then
        return invalid
    end if
    ' Initialize an empty associative array to hold the inverted key-value pairs
    invertedAA = {}
    ' Iterate over each key-value pair in the original associative array
    for each key in originalAA.keys()
        ' Assign the value as the new key and the key as the new value in the inverted array
        invertedAA[toString(originalAA[toString(key)])] = key
    end for
    ' Return the newly created inverted associative array
    return invertedAA
end function

' * Checks if the supplied value is a valid AssociativeArray type
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isAA(value as dynamic) as boolean
    return type(value) = "roAssociativeArray"
end function

' * Checks if the supplied value is a valid Array type
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isArray(value as dynamic) as boolean
    return type(value) = "roArray"
end function

' * Checks if the supplied value is a valid unempty Array like type
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isArrayLike(value as dynamic) as boolean
    return isNonEmptyArray(value) OR isNonEmptyString(value)
end function

' * Checks if the supplied value is a valid Boolean type
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isBoolean(value as Dynamic) as Boolean
    valueType = type(value)
    return (valueType = "Boolean") OR (valueType = "roBoolean")
end function

' * Checks if the supplied value is a valid ByteArray type
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isByteArray(value as dynamic)
    return type(value) = "roByteArray"
end function

' * Alias to isDate function
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isDate(value as Dynamic) as Boolean
    return isDateTime(value)
end function

' * Checks if the supplied value is a valid date time type
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isDateTime(value as Dynamic) as Boolean
    return ("roDateTime" = type(value))
end function

' * Checks if the supplied value is a valid Double type
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isDouble(value as Dynamic) as Boolean
    valueType = type(value)
    return (valueType = "Double") OR (valueType = "roDouble") OR (valueType = "roIntrinsicDouble")
end function

' * Alias to isNode function
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @param {String} subType An optional subType parameter to further refine the check
' * @return {Boolean} Results of the check
function isElement(value as Dynamic, subType = "" as String) as Boolean
    return isNode(value, subtype)
end function

' * Checks if a value is empty.
' * @since 0.0.21
' * @category Lang
function isEmpty(value as dynamic)
    if isInvalid(value) OR isNumber(value) OR isBoolean(value) then
        return true
    else if isAA(value) then
        return NOT isNonEmptyAA(value)
    else if isArray(value) then
        return NOT isNonEmptyArray(value)
    else if isString(value) then
        return isEmptyString(value)
    end if
    return true
end function

' * Checks if the supplied value is a valid String type and is not empty
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isEmptyString(value as Dynamic) as Boolean
    return isString(value) AND value = ""
end function

' * Checks if the supplied values are the same.
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} valueOne - First value.
' * @param {Dynamic} valueTwo - Second value.
' * @return {Boolean} True if the values are the same and false if not or if any of the values are a type that could not be compared.
function isEqual(valueOne as Dynamic, valueTwo as Dynamic) as Boolean
    ' If the first argument is true we don't need to check the follwing conditionals
    if internal_canBeCompared(valueOne, valueTwo) then
        return (valueOne = valueTwo)
    else if isNode(valueOne) then
        if isNode(valueTwo) then
            return valueOne.isSameNode(valueTwo)
        end if
    else if isAA(valueOne) then
        if isAA(valueTwo) AND (join(valueOne.keys(), ",") = join(valueTwo.keys(), ",")) then
            return (formatJson(valueOne) = formatJson(valueTwo))
        end if
    else if isArray(valueOne) then
        if isArray(valueTwo) AND (valueOne.count() = valueTwo.count()) then
            return (formatJson(valueOne) = formatJson(valueTwo))
        end if
    end if
    return false
end function

' * Checks if the supplied values are the same.
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} valueOne - First value.
' * @param {Dynamic} valueTwo - Second value.
' * @return {Boolean} True if the values are the same and false if not or if any of the values are a type that could not be compared.
function isEqualWith(valueOne as Dynamic, valueTwo as Dynamic, customizer = invalid) as Boolean
    ' If the first argument is true we don't need to check the follwing conditionals
    ' TODO: revisit this agressively
    if internal_canBeCompared(valueOne, valueTwo) then
        return customizer(valueOne, valueTwo)
    else if isNode(valueOne) then
        if isNode(valueTwo) then
            valueOne = valueOne.getFields()
            valueOne.delete("change")
            valueOne.delete("focusable")
            valueOne.delete("focusedChild")
            valueOne.delete("ready")
            valueTwo = valueTwo.getFields()
            valueTwo.delete("change")
            valueTwo.delete("focusable")
            valueTwo.delete("focusedChild")
            valueTwo.delete("ready")
            return isEqualWith(valueOne, valueTwo, customizer)
        end if
    else if isAA(valueOne) then
        if isAA(valueTwo) AND (valueOne.keys().count() = valueTwo.keys().count()) then
            keys = valueOne.keys()
            for each key in keys
                if customizer(valueOne[key], valueTwo[key]) then
                    return true
                end if
            end for
        end if
    else if isArray(valueOne) then
        if isArray(valueTwo) AND (valueOne.count() = valueTwo.count()) then
            for i = 0 to valueOne.count() - 1
                if customizer(valueOne[i], valueTwo[i]) then
                    return true
                end if
            end for
        end if
    end if
    return false
end function

' * Assesses the passed object to determine if it is an Error Object.
' TODO: MORE SUPPORT - TRY/CATCH?
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value - the object to assess
' * @return {Boolean} True if the object represents and error.
function isError(value as dynamic) as boolean
    if isAA(value) then
        if isNonEmptyString(value.status) AND stringIncludes(value.status, "error") then
            return true
        end if
        errorCodes = internal_getConstants().BrightScriptErrorCodes
        if hasKeys(value, [
            "number"
            "message"
            "exception"
        ]) then
            for each errorCode in errorCodes
                if value.number = errorCode then
                    return true
                end if
            end for
        end if
    end if
    return false
end function

' * Checks if `value` is a finite primitive number.
' * @since 0.0.21
' * @category Lang
function isFinite(value as dynamic) as boolean
    if NOT isNumber(value) then
        return false
    end if
    constants = internal_getConstants()
    if gt(value, constants.max_int) OR lt(value, constants.min_int) then
        return false
    end if
    return true
end function

' * Checks if the supplied value is a valid Float type
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isFloat(value as Dynamic) as Boolean
    valueType = type(value)
    return (valueType = "Float") OR (valueType = "roFloat")
end function

' * Checks if the supplied value is a valid Function type
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isFunction(value as Dynamic) as Boolean
    valueType = type(value)
    return (valueType = "roFunction") OR (valueType = "Function")
end function

' * Checks if the supplied value is a valid Integer type
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isInteger(value as Dynamic) as boolean
    valueType = type(value)
    return (valueType = "Integer") OR (valueType = "roInt") OR (valueType = "roInteger") OR (valueType = "LongInteger")
end function

' * Checks if the supplied value is Invalid
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isInvalid(value as Dynamic) as Boolean
    return NOT isNotInvalid(value)
end function

' * Checks if value is a valid array-like length
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isLength(value as dynamic) as boolean
    if isInteger(value) AND isFinite(value) AND gte(value, 0) then
        return true
    end if
    return false
end function

' * Alias to isArray function
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isMap(value as dynamic) as boolean
    return isArray(value)
end function

' * Method determines whether the passed value is NaN and its type is a valid number
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value - The variable to be checked
' * @return {Boolean} Results of the check
function isNaN(value as dynamic) as boolean
    return NOT isNumber(value)
end function

' * Checks if the supplied value is a valid Node type
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @param {String} subType An optional subType parameter to further refine the check
' * @return {Boolean} Results of the check
function isNode(value as Dynamic, subType = "" as String) as Boolean
    if type(value) &lt;> "roSGNode" then
        return false
    end if
    if subType &lt;> "" then
        return value.isSubtype(subType)
    end if
    return true
end function

' * Checks if the supplied value is a valid and populated AssociativeArray type
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isNonEmptyAA(value as dynamic)
    return isAA(value) AND value.keys().count() > 0
end function

' * Checks if the supplied value is a valid Array type and not empty
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isNonEmptyArray(value as Dynamic) as Boolean
    return (isArray(value) AND NOT value.isEmpty())
end function

' * Checks if the supplied value is a valid String type and is not empty
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isNonEmptyString(value as Dynamic) as Boolean
    return isString(value) AND value &lt;> ""
end function

' * Checks if the supplied value is not Invalid or uninitialized
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isNotInvalid(value as Dynamic) as Boolean
    return (type(value) &lt;> "&lt;uninitialized>" AND value &lt;> Invalid)
end function

' * Alias to isInvalid function
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isNull(value as Dynamic) as Boolean
    return NOT isNotInvalid(value)
end function

' * Checks if the supplied value is a valid number type
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isNumber(obj as Dynamic) as Boolean
    if (isInteger(obj)) then
        return true
    end if
    if (isFloat(obj)) then
        return true
    end if
    if (isDouble(obj)) then
        return true
    end if
    return false
end function

' * Checks if the supplied value is a valid String type
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be checked
' * @return {Boolean} Results of the check
function isString(value as dynamic)
    valueType = type(value)
    return (valueType = "String") OR (valueType = "roString")
end function

' * Converts all elements in array into a string separated by separator.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to convert
' * @param {String} separator - The element separator
' * @return {Array} Returns the joined string
function join(array = [] as object, separator = "" as string)
    return clone(array).join(separator)
end function

' * Converts a string to kebab case.
' * @since 0.0.21
' * @category String
function kebabCase(value = "" as string)
    value = value.replace("-", " ").replace("_", " ")
    value = value.trim()
    valueArray = value.split(" ")
    return lcase(join(valueArray, "-"))
end function

' * Creates an object composed of keys generated from the results of running each element of collection thru iteratee. The corresponding value of each key is the last element responsible for generating the key. The iteratee is invoked with one argument: (value).
' * @since 0.0.24
' * @category Collection
' * @param {Dynamic} collection - The collection to sample
' * @param {String} key - The iteratee to transform keys.
' * @returns {Dynamic} - Returns the composed aggregate object.
function keyBy(collection = invalid as dynamic, key = "" as string) as object
    returnObject = {}
    if not isArray(collection) then
        return returnObject
    end if
    for each item in collection
        if isAA(item) and item.doesExist(key) then
            newKey = toString(item[key])
            returnObject[newKey] = item
        end if
    end for
    return returnObject
end function

' * Gets the last element of array.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to query
' * @return {Dynamic} Returns the last element of array
function last(array = []) as dynamic
    return array[array.count() - 1]
end function

' * This method is like rodash.indexOf except that it iterates over elements of array from right to left.
' * @since 0.0.23
' * @category Array
' * @param {Array} array - The array to query
' * @param {Dynamic} value - The value to search for
' * @param {Integer} fromIndex - The index to search from
' * @return {Dynamic} Returns the index of the matched value, else -1
function lastIndexOf(array = [] as object, value = invalid as dynamic, fromIndex = invalid as dynamic) as dynamic
    if NOT isArray(array) then
        return -1
    end if
    if isInvalid(fromIndex) then
        fromIndex = array.count() - 1
    else if fromIndex &lt; 0 then
        fromIndex = array.count() + fromIndex
    end if
    ' Ensure fromIndex is within valid bounds
    fromIndex = clamp(fromIndex, 0, array.count() - 1)
    for i = fromIndex to 0 step -1
        if isEqual(array[i], value) then
            return i
        end if
    end for
    return -1
end function

' * Converts a string to lower case.
' * @since 0.0.21
' * @category String
function lowerCase(value = "" as string)
    value = value.replace("-", " ").replace("_", " ")
    value = value.trim()
    valueArray = value.split(" ")
    return lcase(join(valueArray, " "))
end function

' * Converts the first character of string to lower case.
' * @since 0.0.21
' * @category String
function lowerFirst(value = "" as string)
    value = value.trim()
    valueArray = value.split("")
    valueArray[0] = lcase(valueArray[0])
    return join(valueArray)
end function

' * Checks if value is less than other.
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value - The value to compare.
' * @param {Dynamic} other - The other value to compare.
' * @returns {Boolean} - Returns `true` if the value is less than other, else `false`.
function lt(value as dynamic, other as dynamic)
    return value &lt; other
end function

' * Checks if value is less than or equal to other.
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value - The value to compare.
' * @param {Dynamic} other - The other value to compare.
' * @returns {Boolean} - Returns `true` if the value is less than or equal to other, else `false`.
function lte(value as dynamic, other as dynamic)
    return value &lt;= other
end function

' * Creates an array of values by running each element in collection thru iteratee. The iteratee is invoked with three arguments:(value, index|key, collection)
' * @since 0.0.21
' * @category Collection
' * @param {Dynamic} collection - The collection to iterate over
' * @param {Dynamic} iteratee - The function invoked per iteration
' * @return {Array} Returns the new mapped array
function map(collection = {} as dynamic, iteratee = invalid as dynamic)
    if NOT isArray(collection) then
        return []
    end if
    returnArray = []
    for each item in collection
        if isString(iteratee) then
            if isAA(item) then
                returnArray.push(item[iteratee])
            end if
        else if isFunction(iteratee) then
            returnArray.push(iteratee(item))
        end if
    end for
    return returnArray
end function

' * Computes the maximum value of array. If array is empty or falsey, invalid is returned.
' * @since 0.0.21
' * @category Math
' * @param {Array} array - The array to iterate over
' * @return {Dynamic} Returns the maximum value
function max(array = [] as object) as dynamic
    return maxBy(array)
end function

' * Computes the maximum value of array. If array is empty or falsey, invalid is returned.
' * @since 0.0.21
' * @category Math
' * @param {Array} array - The array to iterate over
' * @return {Dynamic} Returns the maximum value
function maxBy(array = [] as object, iteratee = invalid) as dynamic
    if isEmpty(array) then
        return invalid
    end if
    maxValue = invalid
    if isInvalid(iteratee) then
        maxValue = internal_getConstants().min_int
        for each value in array
            if gt(value, maxValue) then
                maxValue = value
            end if
        end for
    else if isFunction(iteratee) AND isAA(array[0]) then
        maxValue = array[0]
        for each value in array
            if gt(iteratee(value), iteratee(maxValue)) then
                maxValue = value
            end if
        end for
    else if isString(iteratee) AND isAA(array[0]) then
        maxValue = array[0]
        for each value in array
            if gt(value[iteratee], maxValue[iteratee]) then
                maxValue = value
            end if
        end for
    end if
    return maxValue
end function

' * Computes the mean of the values in array.
' * @since 0.0.21
' * @category Math
' * @param {Array} array - The array to iterate over
' * @return {Dynamic} Returns the mean value
function mean(array)
    return meanBy(array)
end function

' * This method is like `rodash.mean` except that it accepts `iteratee` which is invoked for each element in array to generate the value to be averaged. The iteratee is invoked with one argument: (value).
' * @since 0.0.21
' * @category Math
' * @param {Array} array - The array to iterate over
' * @param {Function} iteratee - The iteratee invoked per element
' * @return {Dynamic} Returns the mean value
function meanBy(array, iteratee = invalid)
    if isEmpty(array) then
        return invalid
    end if
    return divide(sumBy(array, iteratee), array.count())
end function

' * Computes the minimum value of array. If array is empty or falsey, invalid is returned.
' * @since 0.0.21
' * @category Math
' * @param {Array} array - The array to iterate over
' * @return {Dynamic} Returns the minumum value
function min(array = [] as object) as dynamic
    return minBy(array)
end function

' * Computes the minimum value of array. If array is empty or falsey, invalid is returned.
' * @since 0.0.21
' * @category Math
' * @param {Array} array - The array to iterate over
' * @return {Dynamic} Returns the maximum value
function minBy(array = [] as object, iteratee = invalid) as dynamic
    if isEmpty(array) then
        return invalid
    end if
    minValue = invalid
    if isInvalid(iteratee) then
        minValue = internal_getConstants().max_int
        for each value in array
            if lt(value, minValue) then
                minValue = value
            end if
        end for
    else if isFunction(iteratee) AND isAA(array[0]) then
        minValue = array[0]
        for each value in array
            if lt(iteratee(value), iteratee(minValue)) then
                minValue = value
            end if
        end for
    else if isString(iteratee) AND isAA(array[0]) then
        minValue = array[0]
        for each value in array
            if lt(value[iteratee], minValue[iteratee]) then
                minValue = value
            end if
        end for
    end if
    return minValue
end function

' * Multiplies two numbers.
' * @since 0.0.21
' * @category Math
' * @param {Dynamic} multiplier - The first number in a multiplication.
' * @param {Dynamic} multiplicand - The second number in a multiplication.
' * @returns {Dynamic} - Returns the product of the two numbers.
function multiply(multiplier as dynamic, multiplicand as dynamic) as dynamic
    if isNaN(multiplier) OR isNaN(multiplicand)
        return 0
    end if
    return multiplier * multiplicand
end function

' * Gets the number of milliseconds that have elapsed since the Unix epoch (1 January 1970 00:00:00 UTC).
' * @since 0.0.21
' * @category Date
' * @returns {Object} - The number of milliseconds that have elapsed since the Unix epoch.
function now() as object
    dateObj = internal_getDateObject()
    return {
        "utc": dateObj.utc.asSeconds() + dateObj.utc.getMilliseconds()
        "local": dateObj.utc.asSeconds() + dateObj.utc.getMilliseconds()
    }
end function

' * The opposite of rodash.pick; this method creates an object composed of the own and inherited enumerable property paths of object that are not omitted.
' * @since 0.0.23
' * @category Object
' * @param {Object} object - The source object.
' * @param {Array} paths - The property paths to omit.
' * @returns {Dynamic} - Returns the new object.
function omit(object as Object, paths = [] as Object) as Dynamic
    result = {}
    for each key in object.keys()
        if indexOf(paths, key) = -1 then
            result[key] = object[key]
        end if
    end for
    return result
end function

' * This method is like `sortBy` except that it allows specifying the sort
' * orders of the iteratees to sort by. If `orders` is unspecified, all values
' * are sorted in ascending order. Otherwise, specify an order of "desc" for
' * descending or "asc" for ascending sort order of corresponding values.
' * You may also specify a compare function for an order.
' * @since 0.0.21
' * @category Array
' * @param {Dynamic} collection - The collection to shuffle
' * @param {Dynamic} [iteratee] - The iteratees to sort by
' * @param {Dynamic} [orders] - The sort orders of `iteratees`.
' * @return {Array} Returns the new ordered array
function orderBy(collection, iteratees, orders) as Object
    if isInvalid(collection) OR NOT isArray(collection) then
        return []
    end if
    if not isArray(iteratees) then
        iteratees = bslib_ternary(iteratees = invalid, [], [
            iteratees
        ])
    end if
    if not isArray(orders) then
        orders = bslib_ternary(orders = invalid, [], [
            orders
        ])
    end if
    returnCollection = clone(collection)
    n = returnCollection.Count()
    for i = 0 to n - 2
        for j = 0 to n - i - 2
            if internal_orderByCompare(returnCollection[j], returnCollection[j + 1], iteratees, orders)
                ' Swap returnCollection[j] and returnCollection[j + 1]
                temp = returnCollection[j]
                returnCollection[j] = returnCollection[j + 1]
                returnCollection[j + 1] = temp
            end if
        end for
    end for
    return returnCollection
end function

' * Add padding to the supplied value after converting to a string. For example "1" to "01".
' * @since 0.0.22
' * @category String
' * @param {String} value The value to add padding to.
' * @param {Integer} padLength The minimum output string length.
' * @param {String} paddingCharacter The string to use as padding.
' * @return {String} Resulting padded string.
function padString(value as Dynamic, padLength = 2 as Integer, paddingCharacter = "0" as Dynamic) as String
    value = toString(value)
    while value.len() &lt; padLength
        value = paddingCharacter + value
    end while
    return value
end function


' * Alias to `rodsah.padString`
' * @since 0.0.21
' * @category String
function paddString(value as Dynamic, padLength = 2 as Integer, paddingCharacter = "0" as Dynamic) as String
    return padString(value, padLength, paddingCharacter)
end function

' * Creates an object composed of the picked object properties.
' * @since 0.0.23
' * @category Object
' * @param {Object} object - The object to pick from.
' * @param {Array} paths - The property paths to pick.
' * @returns {Dynamic} - Returns the picked value.
function pick(object as Object, paths = [] as Object) as Dynamic
    picked = {}
    object = clone(object)
    for each key in paths
        if object.doesExist(key) then
            picked[key] = object[key]
        end if
    end for
    return picked
end function

' * Generates a random number between the lower and upper bounds.
' * @since 0.0.21
' * @category Number
function random(lower = 0 as dynamic, upper = 1 as dynamic, floating = invalid) as dynamic
    offset = rnd(0)
    if (isBoolean(upper)) then
        floating = upper
        upper = 1
    end if
    inputValueIsFloat = isFloat(lower) OR isFloat(upper)
    respectFloating = isBoolean(floating) OR inputValueIsFloat
    if respectFloating AND isInvalid(floating) then
        floating = inputValueIsFloat
    end if
    roundLower = rnd(lower)
    roundUpper = rnd(upper)
    roundValue = add(roundLower, roundUpper)
    if gt(roundValue, upper) then
        roundValue = roundUpper
    end if
    if lt(roundValue, lower) then
        roundValue = roundLower
    end if
    if isInvalid(floating) OR (respectFloating AND NOT floating) then
        roundValue = cint(roundValue)
    else
        roundValue = ((roundValue - offset).toStr() + "." + offset.toStr()).toFloat()
    end if
    return roundValue
end function

' * Reduces collection to a value which is the accumulated result of running each element in collection thru iteratee, where each successive invocation is supplied the return value of the previous. If accumulator is not given, the first element of collection is used as the initial value. The iteratee is invoked with four arguments:(accumulator, value, index|key, collection).
' * @since 0.0.21
' * @category Collection
' * @param {Dynamic} collection - The collection to iterate over
' * @param {Dynamic} iteratee - The function invoked per iteration
' * @param {Integer} accumulator - The initial value
' * @return {Array} Returns the accumulated value
function reduce(collection = invalid as dynamic, iteratee = invalid as dynamic, accumulator = invalid as dynamic)
    result = accumulator
    if isInvalid(iteratee) then
        return collection
    else if isFunction(iteratee) then
        if isArray(collection) then
            for each item in collection
                result = iteratee(result, item)
            end for
        else if isAA(collection) then
            for each key in collection.keys()
                item = collection[key]
                result = iteratee(result, item, key)
            end for
        end if
    end if
    return result
end function

' * Computes number rounded up to precision.
' * @since 0.0.21
' * @category Math
' * @param {Float} num - The number to round.
' * @param {Dynamic} precision - The precision to round to.
' * @returns {Dynamic} - Returns the rounded number.
function round(num as Float, precision = 0 as Integer) as Float
    if num >= 0 then
        return floor(num + 0.5 * 10 ^ (-precision), precision)
    else
        return ceil(num - 0.5 * 10 ^ (-precision), precision)
    end if
end function

' * Gets a random element from collection.
' * @since 0.0.23
' * @category Collection
' * @param {Dynamic} collection - The collection to sample
' * @returns {Dynamic} - Returns the random element
function sample(collection = invalid as dynamic)
    if not isArray(collection) then
        return invalid
    end if
    return collection[random(0, collection.count() - 1)]
end function

' * Gets n random elements at unique keys from collection up to the size of collection.
' * @since 0.0.23
' * @category Collection
' * @param {Dynamic} collection - The collection to sample
' * @param {Integer} n - The number of elements to sample
' * @returns {Dynamic} - Returns the random elements.
function sampleSize(collection as Object, n as Integer) as Object
    ' Create a copy of the original array to avoid modifying it
    copyCollection = clone(collection)
    ' Create an array to store the sampled elements
    result = []
    ' Determine the smaller value between n and the length of the array
    count = copyCollection.count()
    if n &lt; count
        count = n
    end if
    while result.count() &lt; count
        index = random(0, copyCollection.count() - 1)
        result.push(copyCollection[index])
        copyCollection.delete(index)
    end while
    return result
end function

' * Used to set a nested String value in the supplied object
' * @since 0.0.21
' * @category Object
' * @param {Object} aa - Object to drill down into.
' * @param {String} keyPath - A dot notation based string to the expected value.
' * @param {Dynamic} value - The value to be set.
' * @return {Boolean} True if set successfully.
function set(aa as Object, keyPath as String, value as Dynamic) as Boolean
    if NOT isAA(aa) then
        return false
    end if
    level = aa
    keys = internal_sanitizeKeyPath(keyPath).tokenize(".")
    while keys.count() > 1
        key = keys.shift()
        if NOT isAA(level[key]) then
            level[key] = {}
        end if
        level = level[key]
    end while
    finalKey = keys.shift()
    level[finalKey] = value
    return true
end function

' * Creates an array of shuffled values, using a version of the Fisher-Yates shuffle.
' * @since 0.0.21
' * @category Collection
' * @param {Dynamic} collection - The collection to shuffle
' * @return {Array} Returns the new shuffled array
function shuffle(collection = [] as dynamic)
    if isInvalid(collection) OR (NOT isArray(collection) AND NOT isAA(collection)) then
        return []
    end if
    if isAA(collection) then
        collection = toArray(collection)
    end if
    length = collection.count() - 1
    if isEqual(length, -1) then
        return []
    end if
    index = -1
    lastIndex = length - 1
    result = clone(collection)
    while index &lt; length
        index++
        rand = index + floor(random(0, 1, true) * (lastIndex - index + 1))
        value = result[rand]
        result[rand] = result[index]
        result[index] = value
    end while
    return result
end function

' * Gets the size of collection by returning its length for array-like values or the number of own enumerable string keyed properties for objects.
' * @since 0.0.21
' * @category Collection
' * @param {Dynamic} collection - The collection to inspect
' * @return {Integer} Returns the collection size.
function size(collection = invalid as dynamic)
    if isInvalid(collection) OR (NOT isArray(collection) AND NOT isAA(collection) AND NOT isString(collection)) then
        return []
    end if
    if isAA(collection) then
        collection = toArray(collection)
    else if isString(collection) then
        return collection.len()
    end if
    return collection.count()
end function

' * Creates a slice of array from start up to, but not including, end.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to slice
' * @param {Integer} startPos - The start position
' * @param {Integer} endPos - The end position
' * @return {Dynamic} Returns the slice of array
function slice(array = [] as object, startPos = 0, endPos = invalid)
    if NOT isArray(array) then
        return invalid
    end if
    if isNotInvalid(endPos) then
        endPos = endPos - 1
    end if
    array = clone(array)
    arraySize = array.count()
    lastIndex = arraySize - 1
    slicedArray = []
    if startPos &lt; 0 then
        startPos = arraySize + startPos
    end if
    if endPos = invalid then
        endPos = lastIndex
    end if
    if endPos &lt; 0 then
        endPos = arraySize + endPos
    end if
    if endPos >= arraySize then
        endPos = lastIndex
    end if
    if startPos >= arraySize OR startPos > endPos then
        return slicedArray
    end if
    for i = startPos to endPos
        slicedArray.push(array[i])
    end for
    return slicedArray
end function

' * Creates an array of elements, sorted in ascending order by the results of running each element in a collection thru each iteratee. This method performs a stable sort, that is, it preserves the original sort order of equal elements. The iteratees are invoked with one argument: (value).
' * @since 0.0.21
' * @category Collection
' * @param {Dynamic} collection - The collection to sort
' * @param {Dynamic} iteratee - The iteratees to sort by
' * @return {Array} Returns the new sorted array
function sortBy(collection = invalid as dynamic, iteratee = invalid as dynamic)
    if isInvalid(collection) OR NOT isArray(collection) then
        return collection
    end if
    returnCollection = clone(collection)
    if isArray(iteratee) then
        for each iteration in iteratee
            if isString(iteration) then
                returnCollection.sortBy(iteration)
            end if
        end for
    else if isFunction(iteratee) then
        key = ""
        for each aa in collection
            for each key in aa.keys()
                if isEqual(aa[key], iteratee(aa)) then
                    exit for
                end if
            end for
            if NOT key = "" then
                exit for
            end if
        end for
        if NOT key = "" then
            returnCollection.sortBy(key)
        end if
    end if
    return returnCollection
end function

' * Uses a binary search to determine the lowest index at which value should be inserted into array in order to maintain its sort order.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The sorted array to inspect
' * @return {Object} Returns the index at which value should be inserted into array
function sortedIndex(array = [] as object, value = 0 as integer)
    for i = 0 to array.count() - 1
        item = array[i]
        nextItem = array[i + 1]
        if isNotInvalid(nextItem) then
            if (item >= value AND value &lt;= nextItem) then
                return i
            end if
        end if
    end for
    return i
end function

' * Check for the existence of a given sub string
' * @since 0.0.21
' * @category String
' * @param {String} value The string to search
' * @param {String} subString The sub string to search for
' * @return {Boolean} Results of the search
function stringIncludes(value as String, subString as String) as Boolean
    return stringIndexOf(value, subString) > -1
end function

' * Finds the sub string index position
' * @since 0.0.21
' * @category String
' * @param {String} value The string to search
' * @param {String} subString The sub string to search for
' * @return {Integer} Results of the search
function stringIndexOf(value as String, subString as String) as Integer
    return value.Instr(subString)
end function

' * Subtract two numbers.
' * @since 0.0.21
' * @category Math
' * @param {Integer} minuend - The first number in a subtraction
' * @param {Integer} subtrahend - The second number in a subtraction
' * @return {Integer} Returns the difference.
function subtract(minuend as dynamic, subtrahend as dynamic) as dynamic
    if isNaN(minuend) OR isNaN(subtrahend)
        return 0
    end if
    return minuend - subtrahend
end function

' * Computes the sum of the values in an array.
' * @since 0.0.21
' * @category Math
' * @param {Array} array - The array to sum
' * @return {Integer} Returns the sum of the values in the array
function sum(array as object)
    sumValue = 0
    for each item in array
        sumValue += item
    end for
    return sumValue
end function

' * This method is like `sum` except that it accepts `iteratee` which is invoked for each element in array to generate the value to be summed.
' * The iteratee is invoked with one argument: (value).
' * @since 0.0.21
' * @category Math
' * @param {Array} array - The array to iterate over
' * @param {Function} iteratee - The iteratee invoked per element
' * @return {Integer} Returns the sum
function sumBy(array = [] as object, iteratee = invalid) as dynamic
    if isEmpty(array) then
        return invalid
    end if
    sumValue = invalid
    if isInvalid(iteratee) then
        sumValue = 0
        for each value in array
            sumValue += value
        end for
    else if isFunction(iteratee) AND isAA(array[0]) then
        sumValue = 0
        for each value in array
            sumValue += iteratee(value)
        end for
    else if isString(iteratee) AND isAA(array[0]) then
        sumValue = 0
        for each value in array
            sumValue += value[iteratee]
        end for
    end if
    return sumValue
end function

' * Creates a slice of array with n elements taken from the beginning
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The sorted array to query
' * @param {Integer} n - The number of elements to take
' * @return {Object} Returns the slice of array
function take(array = [] as object, n = invalid as dynamic) as object
    if isInvalid(n) then
        n = 1
    end if
    if NOT isNonEmptyArray(array) OR n = 0 then
        return []
    end if
    return slice(array, 0, n)
end function

' * Creates a slice of array with n elements taken from the end
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The sorted array to query
' * @param {Integer} n - The number of elements to take
' * @return {Object} Returns the slice of array
function takeRight(array = [] as object, n = invalid as dynamic) as object
    if isInvalid(n) then
        n = 1
    end if
    if NOT isNonEmptyArray(array) OR n = 0 then
        return []
    end if
    length = array.count()
    startPos = length - n
    if startPos &lt; 0 then
        startPos = 0
    end if
    return slice(array, startPos, length)
end function

' * Invokes the iteratee n times, returning an array of the results of each invocation. The iteratee is invoked with one argument; (index).
' * @since 0.0.24
' * @category Utils
' * @param {Integer} n - The number of times to invoke iteratee.
' * @param {Function} iteratee - The function invoked per iteration.
' * @return {Array} Returns the array of results.
function times(n = 0 as Integer, iteratee = invalid as dynamic) as object
    returnArray = []
    if not isInteger(n) then
        return returnArray
    end if
    for i = 1 to n
        returnArray.push(iteratee)
    end for
    return returnArray
end function

' * Attempts to convert the supplied value to a array.
' * @since 0.0.21
' * @category Array
' * @todo Add more support for other types.
' * @param {Dynamic} value The value to convert.
' * @return {Object} Results of the conversion.
function toArray(aa as Object) as Object
    array = []
    for each key in aa
        value = aa[key]
        index = get(value, "index")
        if isNumber(index) then
            array.setEntry(index, value)
        else
            array.push(value)
        end if
    end for
    return array
end function

' * Converts a date object to an ISO string
' * @since 0.0.21
' * @category Date
' * @return {String} Returns the date object as an ISO string
function toISOString(dateObj = invalid as dynamic) as string
    if isDate(dateObj) then
        return ""
    end if
    return dateObj.toISOString()
end function

' * Attempts to convert the supplied value into a valid number
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The variable to be converted
' * @return {Dynamic} Results of the conversion
function toNumber(value as Dynamic) as Dynamic
    if isNumber(value) then
        return value
    else if isBoolean(value) then
        if value then
            return 1
        end if
        return 0
    end if
    if isString(value) then
        ' TODO: Temporary fix until we figure a better way to avoid val converting 8037667 to 8.03767e+06
        if stringIncludes(value, ".") then
            return val(value)
        else
            return val(value, 10)
        end if
    end if
    return 0
end function

' * Creates an array of own enumerable string keyed-value pairs for object which can be consumed by rodash.fromPairs. If object is a map or set, its entries are returned.
' * @since 0.0.24
' * @category Array
' * @param {Object} obj - The object to query.
' * @return {Array} Returns the key-value pairs.
function toPairs(obj = {} as object) as object
    returnArray = []
    if not isAA(obj) then
        return returnArray
    end if
    for each key in obj.keys()
        returnArray.push([
            key
            obj[key]
        ])
    end for
    return returnArray
end function

' * Attempts to convert the supplied value to a string.
' * @since 0.0.21
' * @category Lang
' * @param {Dynamic} value The value to convert.
' * @return {String} Results of the conversion.
function toString(value as Dynamic) as String
    if isString(value) then
        return value
    end if
    if isNumber(value) then
        return internal_numberToString(value)
    end if
    if isNode(value) then
        return internal_nodeToString(value)
    end if
    if isBoolean(value) then
        return internal_booleanToString(value)
    end if
    if isAA(value) then
        return internal_aaToString(value)
    end if
    if isArray(value) then
        return internal_arrayToString(value)
    end if
    return ""
end function

' * Creates an array of unique values, in order, from all given arrays using SameValueZero for equality comparisons.
' * @since 0.0.21
' * @category Array
' * @param {Array} arrays - The arrays to inspect
' * @return {Object} Returns the new array of combined values
function union(arrays = [] as object) as object
    return uniq(flattenDeep(arrays))
end function

' * Creates a duplicate-free version of an array, using SameValueZero for equality comparisons, in which only the first occurrence of each element is kept. The order of result values is determined by the order they occur in the array.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The array to inspect
' * @return {Object} Returns the new duplicate free array
function uniq(array = [] as object) as object
    returnArray = []
    table = {}
    for each item in array
        key = item.toStr()
        if not table.doesExist(key)
            returnArray.push(item)
            table[key] = true
        end if
    end for
    return returnArray
end function

' * Creates an array of grouped elements, the first of which contains the first elements of the given arrays, the second of which contains the second elements of the given arrays, and so on.
' * @since 0.0.21
' * @category Array
' * @param {Array} arrays - The property identifiers
' * @return {Object} Returns the new array of grouped elements
function zip(arrays = [] as object) as object
    returnArray = []
    for i = 0 to arrays.count() - 1
        array = arrays[i]
        for ii = 0 to array.count() - 1
            if isInvalid(returnArray[ii]) then
                returnArray[ii] = []
            end if
            returnArray[ii].push(array[ii])
        end for
    end for
    return returnArray
end function

' * This method is like rodash.fromPairs except that it accepts two arrays, one of property identifiers and one of corresponding values.
' * @since 0.0.21
' * @category Array
' * @param {Array} array - The property identifiers
' * @param {Array} values - The property identifiers
' * @return {Object} Returns the new object
function zipObject(props = [] as object, values = [] as object) as object
    returnObject = {}
    for i = 0 to props.count() - 1
        returnObject[props[i]] = values[i]
    end for
    return returnObject
end function

' * This method is like rodash.zipObject except that it supports property paths.
' * @ignore
' * @category Array
' * @param {Array} array - The property identifiers
' * @param {Array} values - The property identifiers
' * @return {Object} Returns the new object
function zipObjectDeep(props = [] as object, values = [] as object) as object
    ' COME BACK
end function</code></pre>
        </article>
    </section>




            </div>
            
            <footer class="footer">
                <div class="content has-text-centered">
                    <p>Documentation generated by <a href="https://github.com/jsdoc3/jsdoc">JSDoc 4.0.3</a></p>
                    <p class="sidebar-created-by">
                        <a href="https://github.com/SoftwareBrothers/better-docs" target="_blank">BetterDocs theme</a> provided with <i class="fas fa-heart"></i> by
                        <a href="http://softwarebrothers.co" target="_blank">SoftwareBrothers - JavaScript Development Agency</a>
                    </p>
                </div>
            </footer>
            
        </div>
        <div id="side-nav" class="side-nav">
        </div>
    </div>
<script src="scripts/app.min.js"></script>
<script>PR.prettyPrint();</script>
<script src="scripts/linenumber.js"> </script>

<script src="scripts/search.js"> </script>


</body>
</html>
